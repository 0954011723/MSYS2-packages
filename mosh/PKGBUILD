# Maintainer: James Ross-Gowan <rossymiles@gmail.com>

pkgname=mosh
pkgver=1.2.4
pkgrel=1
pkgdesc="Mobile shell, surviving disconnects with local echo and line editing"
arch=('i686' 'x86_64')
url='http://mosh.mit.edu/'
groups=('net-utils')
license=('GPL3')
depends=('protobuf' 'ncurses' 'zlib' 'libopenssl' 'openssh' 'perl')
makedepends=('protobuf-devel' 'ncurses-devel' 'zlib-devel' 'openssl-devel')
source=("http://${pkgname}.mit.edu/${pkgname}-${pkgver}.tar.gz")
md5sums=('c2d918f4d91fdc32546e2e089f9281b2')
options=(!emptydirs)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --host=${CHOST} \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/conf/bash_completion.d/$pkgname" \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
