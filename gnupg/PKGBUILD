# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gnupg
pkgver=1.4.15
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('curl: gpg2keys_curl')
makedepends=('curl' 'libbz2-devel' 'libcurl-devel' 'libiconv-devel' 'gettext-devel')
depends=('bzip2' 'libcurl' 'libiconv' 'libintl')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        'gnupg-1.4.15-msys2.patch')
sha1sums=('63ebf0ab375150903c65738070e4105200197fd4'
          'SKIP'
          '3a3ef80e6b95d6b314e3accdec84eb7360d22fce')

install=install

conflicts=('gnupg2')
provides=("gnupg=${pkgver}")
replaces=('gnupg2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/gnupg-1.4.15-msys2.patch
  
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/gnupg \
    --enable-maintainer-mode

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # mv -f ${pkgdir}/etc ${pkgdir}/usr/
}
