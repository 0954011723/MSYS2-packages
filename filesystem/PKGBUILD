# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=filesystem
pkgver=2014.11
pkgrel=1
pkgdesc='Base filesystem'
arch=('i686' 'x86_64')
license=('GPL')
url='http://sourceforge.net/projects/msys2/'
groups=('base')
provides=('msys2-base')
replaces=('msys2-base')
install=filesystem.install
backup=('etc/fstab' 'etc/shells' 'etc/profile'
        etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
source=('bash.bash_logout'
        'bash.bashrc'
        'cmd'
        'dep-search'
        'dot.bash_logout'
        'dot.bash_profile'
        'dot.bashrc'
        'dot.inputrc'
        'dot.profile'
        'fstab'
        'mingw32_shell.bat'
        'mingw64_shell.bat'
        'msys2_shell.bat'
        'msys2.ico'
        'nsswitch.conf'
        'profile'
        'profile.lang.sh'
        'profile.tzset.sh'
        'regen-info.sh'
        'shells'
        'start'
        01-devices.post
        03-mtab.post
        05-home-dir.post
        06-windows-files.post
        07-pacman-key.post
        08-xml-catalog.post)
md5sums=('3b95f37af49beb642c8fe174dd1f63fc'
         'b5596462c700d433ac187260d96de22b'
         '9ffd57721725e961ffd5e311d6c2f374'
         'e6a830e08fce049c78272d275f57d57f'
         '42f4400ed2314bd7519c020d0187edc5'
         'e32cd0cf441ce0e8c57f550b0d28b182'
         '7a8cf2e199bc046bcfed1fc75275b15c'
         'ff5b9ce40fb39b9c745f188bde6dabea'
         '162d83283cf4088f00dab39573eb2802'
         'b0eb2ba1c1bc95e678309fd36dac27db'
         'c9e991e47fd0384c913d3f980dc7a4e8'
         '967f819d0dc83b552b6438de7789dfbb'
         '799c5f57175ec472aa8404768981c53f'
         '292ad5cdd78abac9d694cc06819a96fc'
         'afcd730542d7e78ca9a8027a3ed28eb4'
         '05f9a11b82e7299eb9f78c6b3debd606'
         '118fa00617d4d0b2eb337dd565d44494'
         '39c1d2412eb62864c8c07cb2557c7da3'
         '3b6753667b61800db1a30c614efd1ee1'
         'bb51026508581e55bbefe36e2b799108'
         '900c95162b9c3f7a29912408e0579852'
         '7df6a2d0c05e49bf9224fdfbc240a730'
         'c3c57aa78cdb6cd5dea1e51b12cdbc1d'
         'b80fcdfaa3942a09d473286b8397114c'
         'fef301aa9d3357ca034fbb3adf77f0f0'
         '8ab86771cc1180fc52adca9629858243'
         '751ef1e25f070cd8085f9598c783c018')

build() {
  cd ${srcdir}
}

package() {
  cd ${pkgdir}

  #
  # setup root filesystem
  #
  for d in dev etc home usr var opt mingw32 mingw64; do
    install -d -m755 ${d}
  done

  mkdir -p tmp

  # setup /etc
  install -d etc/{fstab.d,skel,profile.d,post-install}
  for f in bash.bashrc bash.bash_logout fstab shells profile nsswitch.conf; do
    install -m644 ${srcdir}/${f} etc/
  done
  
  for f in ${srcdir}/*.post; do
    install -m644 ${f} etc/post-install/
  done

  # user configuration file skeletons
  install -m755 ${srcdir}/dot.bashrc etc/skel/.bashrc
  install -m755 ${srcdir}/dot.inputrc etc/skel/.inputrc
  install -m755 ${srcdir}/dot.bash_profile etc/skel/.bash_profile
  install -m755 ${srcdir}/dot.bash_logout etc/skel/.bash_logout
  
  install -m755 ${srcdir}/profile.tzset.sh etc/profile.d/tzset.sh
  install -m755 ${srcdir}/profile.lang.sh etc/profile.d/lang.sh
  
  install -m644 ${srcdir}/msys2.ico msys2.ico
  install -m755 ${srcdir}/msys2_shell.bat msys2_shell.bat
  install -m755 ${srcdir}/mingw32_shell.bat mingw32_shell.bat
  install -m755 ${srcdir}/mingw64_shell.bat mingw64_shell.bat
  
  # setup /var
  for d in cache/man local opt log/old lib/misc empty; do
    install -d -m755 var/${d}
  done
  mkdir -p var/tmp

  #
  # setup /usr hierarchy
  #
  for d in bin include lib sbin share/misc src; do
    install -d -m755 usr/${d}
  done
  for d in $(seq 8); do
    install -d -m755 usr/share/man/man${d}
  done
  # various shell scripts
  for f in cmd start dep-search; do
    install -m755 ${srcdir}/${f} usr/bin
  done
  install -m755 ${srcdir}/regen-info.sh usr/sbin
  
  #
  # setup /mingw32 hierarchy
  #
  for d in bin etc include lib share; do
    install -d -m755 mingw32/${d}
  done
  
  #
  # setup /mingw64 hierarchy
  #
  for d in bin etc include lib share; do
    install -d -m755 mingw64/${d}
  done
}
