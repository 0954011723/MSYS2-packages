# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gdb
pkgver=7.6.1
pkgrel=1
_gcc_ver=4.8.2
pkgdesc="GNU Debugger (MSYS2 version)"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/gdb/"
groups=('base-devel')
depends=("libiconv" "zlib" "expat" "libiconv" "python2")
#checkdepends=('dejagnu' 'bc')
makedepends=("libiconv-devel" "zlib-devel" "ncurses-devel" "liblzma-devel")
options=('staticlibs' '!distcc' '!ccache')
source=("ftp://mirrors.kernel.org/sources.redhat.com/gdb/releases/gdb-${pkgver}.tar.bz2"
    'gdbinit'
    'gdb-7.6-msys2.patch')
md5sums=('fbc4dab4181e6e9937075b43a4ce2732'
         'dec2439447cb1accf037457b459093f7'
         'f06ddf7417948d03385873ae10e5b5db')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/gdb-7.6-msys2.patch
  
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
    --build=${CHOST} \
    --host=${CHOST} \
    --prefix=/usr \
    --enable-64-bit-bfd \
    --disable-werror \
    --with-system-gdbinit=/etc/gdbinit \
    --with-python=/usr/bin/python2 \
    --without-tcl \
    --without-tk \
    --with-expat \
    --with-libiconv-prefix=/usr \
    --with-zlib \
    --disable-tui
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install "custom" system gdbinit
  install -D -m644 ${srcdir}/gdbinit ${pkgdir}/usr/etc/gdbinit
  sed -i 's|%GCC_NAME%|gcc-'${_gcc_ver}'|g' ${pkgdir}/usr/etc/gdbinit
  
  # these are shipped by binutils
  rm -fr ${pkgdir}/usr/{include,lib}/ ${pkgdir}/usr/share/locale/
  rm -f ${pkgdir}/usr/share/info/{bfd,configure,standards}.info
}
