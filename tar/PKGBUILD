# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=tar
pkgver=1.27
pkgrel=1
pkgdesc="Utility used to store, backup, and transport files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('compression')
depends=('msys2-runtime' 'libiconv' 'libintl' 'sh')
options=('!emptydirs')
install=tar.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        1.25-mode-binary.patch
        tar-1.27-fpending.patch
        tar-1.27-msys2.patch)
md5sums=('3484fc42b7a82b82f2305b2971aa0e56'
         'SKIP'
         '15fcf413ad1fc3fbcce0d85c192096c8'
         '9ec91c6a18b1c594355cea05938e9f2c'
         'e2a133fbf1b02154730a5b2ff193ecc1')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -p2 -i ${srcdir}/1.25-mode-binary.patch
  patch -p1 -i ${srcdir}/tar-1.27-msys2.patch
  patch -p1 -i ${srcdir}/tar-1.27-fpending.patch
  
  autoreconf -fi
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --libexecdir=/usr/lib/tar \
    --enable-backup-scripts
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}
  
package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
