# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Try-Tiny
pkgname=perl-${_realname}
pkgver=0.18
pkgrel=1
pkgdesc="Minimal try/catch with proper localization of \$@"
arch=('any')
url="http://search.cpan.org/dist/Try-Tiny"
groups=('perl-modules')
license=('MIT')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/${_realname}-${pkgver}.tar.gz")
sha1sums=('1910c037e4b7367c58d2e2f06464863bdee2b06a')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
