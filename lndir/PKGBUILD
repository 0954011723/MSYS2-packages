# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=lndir
pkgver=1.0.3
pkgrel=1
pkgdesc="Create a shadow directory of symbolic links to another directory tree"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
groups=('base')
depends=('msys2-runtime')
makedepends=('xproto')
source=(http://xorg.freedesktop.org/archive/individual/util/$pkgname-$pkgver.tar.bz2
        0001-msysize.msys.patch)
md5sums=('a47bcd6cecda1a6ebd3395926ea4f53a'
         '149b0b4868f9dc291301d100e93a16e9')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ${srcdir}/0001-msysize.msys.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --build=${CHOST} \
              --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
