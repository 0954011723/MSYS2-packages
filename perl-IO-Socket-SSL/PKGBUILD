# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=IO-Socket-SSL
pkgname=perl-${_realname}
pkgver=1.984
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/IO-Socket-SSL/"
groups=('perl-modules')
depends=('perl-Net-SSLeay' 'perl' 'perl-URI')
checkdepends=('perl-IO-Socket-INET6')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/S/SU/SULLR/${_realname}-${pkgver}.tar.gz)
sha1sums=('16f45ccb7c605591943465d55fa6fdec65a3e631')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
