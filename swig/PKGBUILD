# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=swig
pkgver=3.0.6
pkgrel=1
pkgdesc="Generate scripting interfaces to C/C++ code"
arch=('i686' 'x86_64')
url="http://www.swig.org/"
license=('custom')
groups=('base-devel')
depends=('zlib' 'libpcre')
makedepends=('zlib-devel' 'pcre-devel')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        swig-2.0.10-msys2.patch)
sha1sums=('901dd1888581289b5fdc6c0f4452109652bcf244'
          '33f14f23175ca2844795a30f867e821896e2f59b')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i ${srcdir}/swig-2.0.10-msys2.patch

  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure -build=${CHOST} --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -D -m644 LICENSE-UNIVERSITIES "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE-UNIVERSITIES
}
