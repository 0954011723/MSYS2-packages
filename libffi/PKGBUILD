# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libffi' 'libffi-devel')
pkgver=3.0.13
pkgrel=2
pkgdesc="Portable, high level programming interface to various calling conventions"
arch=('i686' 'x86_64')
url="http://sourceware.org/libffi/"
groups=('libraries')
license=('MIT')
depends=()
checkdepends=('dejagnu')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz
        libffi-3.0.12-cygwin.patch
        libffi-3.0.13-msys2.patch)
sha1sums=('f5230890dc0be42fb5c58fbf793da253155de106'
          'ab87e474fd698897b4747703daf15bd7457ff2b8'
          '15658b7264b942756a8c81c7ee30cca9faaf721b')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i $srcdir/libffi-3.0.12-cygwin.patch
  patch -p1 -i $srcdir/libffi-3.0.13-msys2.patch
  
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --build=${CHOST} \
    --enable-static \
    --enable-shared
  make
  make DESTDIR="$srcdir/dest" install
}

check() {
  cd $pkgname-$pkgver
  make check
}

package_libffi() {
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
  install -Dm644 $srcdir/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_libffi-devel() {
  pkgdesc="Libffi headers and libraries"
  groups=('development')
  options=('staticlibs')
  depends=("libffi=${pkgver}")

  mkdir -p $pkgdir/usr
  #cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}
