# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libtool' 'libltdl')
pkgver=2.4.6
_gccver=6.3.0
pkgrel=3
pkgdesc="A generic library support script"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/libtool"
license=('GPL')
makedepends=("gcc=${_gccver}")
source=(https://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig}
        #http://alpha.gnu.org/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig}
		#${pkgname}-${pkgver}::git://git.sv.gnu.org/libtool.git
        0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
        0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
        0005-Fix-seems-to-be-moved.patch
        0006-Fix-strict-ansi-vs-posix.patch
        0007-fix-cr-for-awk-in-configure.all.patch
        0009-libtool-2.4.2.418-msysize.patch
        0010-libtool-2.4.2-include-process-h.patch)
sha256sums=('7c87a8c2c8c0fc9cd5019e402bed4292462d00a718a7cd5f11218153bf28b26f'
            'SKIP'
            'df8e905c37eafab5658f62adcb6c6fcabc9555e749e94e2ffb96736edf73e07b'
            '2864796f4b88d53e969c652d77a6867ff6110fa8a680cca90c0b8481a387e8eb'
            'e0eb4331464e653ae30f0baa1641bdfbdabd19e74a8303cfc1832a8e2ab9e593'
            '086afb1c9a8638c8772216938532a3fe0c3124cf98d0580c6c2823e2588902d5'
            '2a94a72c8b0f853eab218249ee049ffaa43b3315b72b2bada1662b31f80f70fc'
            '15bf88076c81221aefd343b84c78cad97b825c161f39cdf0a9190f9ffbc78291'
            '9aca27fa59759ef86f9a370aca93f8c4e0119954fe3991bba9b68eaf76dcee3d')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
  patch -p1 -i ${srcdir}/0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
  patch -p1 -i ${srcdir}/0005-Fix-seems-to-be-moved.patch
  patch -p1 -i ${srcdir}/0006-Fix-strict-ansi-vs-posix.patch
  patch -p1 -i ${srcdir}/0007-fix-cr-for-awk-in-configure.all.patch

  patch -p1 -i ${srcdir}/0009-libtool-2.4.2.418-msysize.patch
  patch -p1 -i ${srcdir}/0010-libtool-2.4.2-include-process-h.patch

}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /lib"
  make -j1
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package_libtool() {
  depends=('sh' "libltdl=${pkgver}" 'tar')
  groups=('base-devel')
  install=libtool.install

  cd ${srcdir}/${pkgbase}-${pkgver}

  make DESTDIR=${pkgdir} install-binSCRIPTS install-man install-info \
                         install-data-local
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool"
  options=('staticlibs')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS
}
