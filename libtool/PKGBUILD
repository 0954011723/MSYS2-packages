# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libtool' 'libltdl')
pkgver=2.4.2
_gccver=4.8.2
pkgrel=3
pkgdesc="A generic library support script"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
makedepends=("gcc=${_gccver}")
source=(ftp://ftp.gnu.org/pub/gnu/libtool/${pkgname}-${pkgver}.tar.xz{,.sig}
        2.4-cygwin-mingw-Create-UAC-manifest-files.patch
        2.4-pass-ldflags.patch
        libtool-2.4.2-msys2.patch
        libtool-2.4.2-include-process-h.patch)
md5sums=('2ec8997e0c07249eb4cbd072417d70fe'
         'SKIP'
         '47f93ccdbe44347b5e929524df52ec4e'
         '25dd8da8027385bbb9eeeb659ce8adaa'
         '5128f5f99c73e249e5bd760a42d871fe'
         'b5deb89b4f80ab8235329e56608eed5b')

prepare() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  patch -p1 -i ${srcdir}/2.4-cygwin-mingw-Create-UAC-manifest-files.patch
  patch -p1 -i ${srcdir}/2.4-pass-ldflags.patch
  patch -p1 -i ${srcdir}/libtool-2.4.2-msys2.patch
  patch -p1 -i ${srcdir}/libtool-2.4.2-include-process-h.patch
}

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  make check
}

package_libtool() {
  depends=('sh' "libltdl=${pkgver}" 'tar' "gcc=${_gccver}")
  groups=('base-devel')
  install=libtool.install

  cd ${srcdir}/${pkgbase}-${pkgver}
  
  make DESTDIR=${pkgdir} install-binSCRIPTS install-man install-info \
                         install-data-local
  rm -r ${pkgdir}/usr/share/libtool/libltdl/
}

package_libltdl() {
  pkgdesc="A system independent dlopen wrapper for GNU libtool"
  options=('staticlibs')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install-libLTLIBRARIES install-includeHEADERS \
                         install-ltdlincludeHEADERS install-data-local
  rm -r ${pkgdir}/usr/share/{aclocal,libtool/config}
}
