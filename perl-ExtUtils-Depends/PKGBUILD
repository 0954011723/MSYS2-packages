# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-Depends
pkgname=perl-${_realname}
pkgver=0.308
pkgrel=2
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/X/XA/XAOC/${_realname}-${pkgver}.tar.gz"
        'ExtUtils-Depends-0.304.patch')
md5sums=('cea7ebb99d05fe47c24a1de043b478f2'
         '8e93a3f630fd2d85840032f0c6cf8f17')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/ExtUtils-Depends-0.304.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
