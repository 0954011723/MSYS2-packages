# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=m4
pkgver=1.4.18
pkgrel=1
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('bash' 'gcc-libs' 'msys2-runtime')
makedepends=('')
install=m4.install
source=(https://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig}
        m4-1.4.17-msys2.patch)
sha256sums=('f2c1e86ca0a404ff281631bdc8377638992744b175afb806e25871a24a934e07'
            'SKIP'
            'ea969a4948ba0f4457446602e991d579e27be6cf825ae9a63341f3fa9ea2bb75')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -p1 -i ${srcdir}/m4-1.4.17-msys2.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-syscmd-shell=/usr/bin/sh
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
