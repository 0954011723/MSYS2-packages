# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=mintty
pkgver=1322
pkgrel=2
pkgdesc="Terminal emulator with native Windows look and feel"
arch=('i686' 'x86_64')
license=('GPL3')
groups=('base')
depends=('sh')
makedepends=('subversion')
url="https://code.google.com/p/mintty/"
source=("mintty"::'svn+http://mintty.googlecode.com/svn/trunk/'
        01-array-bounds.patch
        02-mintty-add-msys-64bit.patch)
#http://mintty.googlecode.com/files/${pkgname}-${pkgver}-src.tar.bz2
sha1sums=('SKIP'
          '087d131896f53635d879959f8f0b538f81fd37a5'
          'ce948d06bb62308e59966c7816301df415cc6074')
#sha1sums=('54160819e1e6d37ef1a69aac0cb909ba10d30175')

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i ${srcdir}/01-array-bounds.patch
  patch -p1 -i ${srcdir}/02-mintty-add-msys-64bit.patch
}

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  mkdir -p ${pkgdir}/usr/{bin,share}
  mkdir -p ${pkgdir}/usr/share/man/man1
  
  install -m755 mintty.exe ${pkgdir}/usr/bin/mintty.exe
  install -m644 docs/mintty.1 ${pkgdir}/usr/share/man/man1/mintty.1
}
