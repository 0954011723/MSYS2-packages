# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=zsh
pkgname=('zsh' 'zsh-doc')
pkgver=5.0.7
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
makedepends=('ncurses-devel' 'pcre-devel' 'libiconv-devel' 'libgdbm-devel')
source=("http://www.zsh.org/pub/zsh-${pkgver}.tar.bz2"
        "http://www.zsh.org/pub/zsh-${pkgver}-doc.tar.bz2"
        'config.guess::http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.guess;hb=HEAD'
        'config.sub::http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.sub;hb=HEAD'
        'zprofile'
        Makefile.in.patch
        add-pwd-W-option.patch)
md5sums=('1cd396ce17952de50b8a89980d617f0a'
         'ec04d46211a20281778ddbbde9aa680f'
         '0fb81517303511f05a01b14f41cec2cf'
         '1528b859cadb0b39f79df96c9f0e8599'
         '24a9335edf77252a7b5f52e079f7aef7'
         'ca9d481e1aa03e864734734d2267afb0'
         'df6860fd5b5b46d59ad03220e9125c9d')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -p0 -i "${srcdir}/Makefile.in.patch"
    patch -p1 -i "${srcdir}/add-pwd-W-option.patch"
    mv -f ${srcdir}/config.guess ${srcdir}/config.sub ${srcdir}/${pkgname}-${pkgver}/

    # Set correct keymap path
    sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

    # Remove unneeded and conflicting completion scripts
    for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
        rm -rf Completion/$_fpath
        sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
    done
    rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
    rm -f  Completion/Unix/Command/_{osc,systemd}
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --docdir=/usr/share/doc/zsh \
        --enable-etcdir=/etc/zsh \
        --enable-zshenv=/etc/zsh/zshenv \
        --enable-zlogin=/etc/zsh/zlogin \
        --enable-zlogout=/etc/zsh/zlogout \
        --enable-zprofile=/etc/zsh/zprofile \
        --enable-zshrc=/etc/zsh/zshrc \
        --enable-maildir-support \
        --with-term-lib='ncursesw' \
        --enable-multibyte \
        --enable-function-subdirs \
        --enable-fndir=/usr/share/zsh/functions \
        --enable-scriptdir=/usr/share/zsh/scripts \
        --with-tcsetpgrp \
        --enable-pcre \
        --disable-cap \
        --enable-zsh-secure-free
    make
}

package_zsh() {
    pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
    depends=('ncurses' 'pcre' 'libiconv' 'gdbm')
    backup=('etc/zsh/zprofile')
    install=zsh.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
    install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
    install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_zsh-doc() {
    pkgdesc='Info, HTML and PDF format of the ZSH documentation'
    install=zsh-doc.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}/" install.info
    install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
