# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgbase=zsh
pkgname=('zsh' 'zsh-doc')
pkgver=5.0.5
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
makedepends=('ncurses-devel' 'pcre' 'libiconv' 'gdbm')
source=("http://sourceforge.net/projects/zsh/files/zsh/${pkgver}/zsh-${pkgver}.tar.bz2"
        'config.guess::http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.guess;hb=HEAD'
        'config.sub::http://git.savannah.gnu.org/gitweb/?p=config.git;a=blob_plain;f=config.sub;hb=HEAD'
        'zprofile::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/zprofile?h=packages/zsh'
        zsh-doc.install
        zsh.install
        Makefile.in.patch)
md5sums=('6156dc2f19b0a067bdbc0fb7f81e2017'
         'SKIP'
         'SKIP'
         'SKIP'
         '2a719c1ff8d61db68164e8a0a57d83c8'
         '92625ebc2c387157c5d05bfe4b04c8b5'
         'ca9d481e1aa03e864734734d2267afb0')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -p0 -i ${srcdir}/Makefile.in.patch
    mv -f ${srcdir}/config.guess ${srcdir}/config.sub ${srcdir}/${pkgname}-${pkgver}/

    # Set correct keymap path
    sed -i 's#/usr/share/keymaps#/usr/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys

    # Remove unneeded and conflicting completion scripts
    for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
        rm -rf Completion/$_fpath
        sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
    done
    rm -f  Completion/Linux/Command/_{pkgtool,rpmbuild,yast}
    rm -f  Completion/Unix/Command/_{osc,systemd}
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
        --docdir=/usr/share/doc/zsh \
        --enable-etcdir=/etc/zsh \
        --enable-zshenv=/etc/zsh/zshenv \
        --enable-zlogin=/etc/zsh/zlogin \
        --enable-zlogout=/etc/zsh/zlogout \
        --enable-zprofile=/etc/zsh/zprofile \
        --enable-zshrc=/etc/zsh/zshrc \
        --enable-maildir-support \
        --with-term-lib='ncursesw' \
        --enable-multibyte \
        --enable-function-subdirs \
        --enable-fndir=/usr/share/zsh/functions \
        --enable-scriptdir=/usr/share/zsh/scripts \
        --with-tcsetpgrp \
        --enable-pcre \
        --disable-cap \
        --enable-zsh-secure-free
    make
}

package_zsh() {
    pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
    depends=('ncurses-devel' 'pcre' 'libiconv' 'gdbm')
    backup=('etc/zsh/zprofile')
    install=zsh.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
    install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
    install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_zsh-doc() {
    pkgdesc='Info, HTML and PDF format of the ZSH documentation'
    install=zsh-doc.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}/" install.info
    install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
