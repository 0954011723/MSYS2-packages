# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Test-Script
pkgname=perl-${_realname}
pkgver=1.07
pkgrel=1
pkgdesc="Basic cross-platform tests for scripts"
arch=('any')
url="http://search.cpan.org/dist/File-Which"
groups=('perl-modules')
depends=('perl' 'perl-IPC-Run3' 'perl-Probe-Perl')
license=('GPL' 'PerlArtistic')
source=("http://www.cpan.org/authors/id/A/AD/ADAMK/${_realname}-${pkgver}.tar.gz"
        'Test-Script-1.07.patch')
md5sums=('f6b5baa6403cd24dac7f023e0ea22384'
         '45b895d82b11eecb4b5892d8362b1a04')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  
  patch -p1 -i ${srcdir}/Test-Script-1.07.patch
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="$pkgdir"
}
