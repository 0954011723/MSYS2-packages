# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=screenfetch
_pkgname=screenFetch
pkgver=3.7.0
pkgrel=1
pkgdesc="Bash screenshot information tool"
arch=('any')
url="https://github.com/KittyKatt/screenFetch"
license=('GPL')
depends=('bash')
#optdepends=('scrot: screenshot taking'
#            'curl: screenshot uploading')
source=(https://github.com/KittyKatt/${_pkgname}/archive/v${pkgver}.tar.gz
        msys2.patch)
sha256sums=('6711fe924833919d53c1dfbbb43f3777d33e20357a1b1536c4472f6a1b3c6be0'
            '727d646199dd35cfe217725f8359a63f3a02797dc5eb57c9133b19ab0708e9c0')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/msys2.patch
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./update-manpage.sh
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -Dm755 screenfetch-dev "${pkgdir}/usr/bin/screenfetch"
  install -Dm644 screenfetch.1 "${pkgdir}/usr/share/man/man1/screenfetch.1"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 CHANGELOG "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
  install -Dm644 README.mkdn "${pkgdir}/usr/share/doc/${pkgname}/README.mkdn"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}
