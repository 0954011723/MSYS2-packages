# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=TermReadKey
pkgname=perl-${_realname}
pkgver=2.30.02
pkgrel=1
pkgdesc="Provides simple control over terminal driver modes"
arch=('i686' 'x86_64')
license=('custom')
depends=('perl')
url="http://search.cpan.org/~stsi/${_realname}/"
groups=('perl-modules')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/ST/STSI/${_realname}-${pkgver}.tar.gz)
md5sums=('69b92c8ddca8691c83823c683cd9838a')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/licenses/$pkgname/"
  head -7 README > "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
