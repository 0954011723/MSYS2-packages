# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=libevent
pkgver=2.1.4
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('BSD')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
source=(${pkgname}-${pkgver}-alpha.tar.gz::"https://github.com/${pkgname}/${pkgname}/archive/release-${pkgver}-alpha.tar.gz")
md5sums=('3db1e1169357a34c0ab5b2aced06e5b1')

prepare() {
  cd ${srcdir}/${pkgname}-release-${pkgver}-alpha
  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgname}-release-${pkgver}-alpha
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/${pkgname}-release-${pkgver}-alpha
  make DESTDIR=${pkgdir} install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
