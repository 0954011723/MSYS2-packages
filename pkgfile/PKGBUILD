# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pkgfile
pkgver=12
pkgrel=1
pkgdesc="a pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/pkgfile"
groups=('base' 'base-devel')
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
options=('emptydirs')
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
		'fix-msys-build.patch')
install=pkgfile.install
md5sums=('3366e5dbf01ebb81197c4fc53c6bb3bf'
         'SKIP'
         '7f8bb120e211d8a9605a1ec77029131b')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i ${srcdir}/fix-msys-build.patch
}

build() {
	[[ -d "$srcdir/build-${CHOST}" ]] && rm -rf "$srcdir/build-${CHOST}"
	mkdir "$srcdir/build-${CHOST}" && cd "$srcdir/build-${CHOST}"
	#cd "$pkgname-$pkgver"

	../$pkgname-$pkgver/configure
	make

	# generate a license file
	sed '/\*\//q' $srcdir/$pkgname-$pkgver/src/pkgfile.c >LICENSE
}

package() {
	cd "$srcdir/build-${CHOST}"

	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	mv "$pkgdir/var" "$pkgdir/usr/"
}
