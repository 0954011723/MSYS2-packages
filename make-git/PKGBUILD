# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=make
pkgname="${_realname}-git"
_ver_base=4.0
pkgver=4.0.2294.8871368
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('msys2-runtime' 'sh' 'guile')
makedepends=('gettext' 'gettext-devel' 'git' 'wget')
provides=("${_realname}")
conflicts=("${_realname}")
install=$_realname.install
source=("$pkgname"::'git://git.savannah.gnu.org/make.git'
        'make-autoconf.patch'
        '0001-output.c-Fix-memory-stomp-when-need-fmtbuf.size.patch'
        'make-bug-41341.patch')
md5sums=('SKIP'
         'e33ecebcf51947bb5ca54309b6e86c5f'
         'b06af775e84a34248bb524104a36bc61'
         '33c00ec9c1a4b0c2d16ef9776bb676c2')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${pkgname}
  patch -p1 -i ${srcdir}/make-autoconf.patch
  patch -p1 -i ${srcdir}/0001-output.c-Fix-memory-stomp-when-need-fmtbuf.size.patch
  patch -p1 -i ${srcdir}/make-bug-41341.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}
  ./configure --build=${CHOST} --prefix=/usr \
    ac_cv_dos_paths=yes --without-libintl-prefix --without-libiconv-prefix
  make -j1 scm-update do-po-update all
}

check() {
  cd ${srcdir}/${pkgname}
  make check
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
}
