# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=make-git
_ver_base=4.0
pkgver=4.0.2298.8411528
pkgrel=1
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
provides=("make")
conflicts=("make")
depends=('msys2-runtime' 'sh' 'guile')
#options=('debug' '!strip')
makedepends=('gettext' 'gettext-devel' 'git' 'wget')
install=$pkgname.install
source=("$pkgname"::'git://git.savannah.gnu.org/make.git'
        'make-autoconf.patch')
md5sums=('SKIP'
         'ccba99fbedbadfff09bb3fe220e9cab8')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${srcdir}/${pkgname}
  patch -p1 -i ${srcdir}/make-autoconf.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}
  ./configure --build=${CHOST} --prefix=/usr \
    ac_cv_dos_paths=yes --without-libintl-prefix --without-libiconv-prefix
  make -j1 scm-update do-po-update all
}

check() {
  cd ${srcdir}/${pkgname}
  make check
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
}
