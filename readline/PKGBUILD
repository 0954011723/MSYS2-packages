# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=readline
pkgname=('libreadline' 'libreadline-devel')
_basever=6.3
_patchlevel=000 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('i686' 'x86_64')
groups=('libraries')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('ncurses')
makedepends=('ncurses-devel')
#backup=('etc/inputrc')
options=('!emptydirs')
install=readline.install
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
		readline-6.3-cygwin.patch
        readline-6.3-msys2.patch
		readline-6.3-dispatch-multikey.patch
		readline-6.3-vi-last.patch)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi
md5sums=('33c8fb279e981274f485fd91da77e94a'
         'SKIP'
         '1bc758a31a4bdcccbfbf7bcf334edc4a'
         '303e24bd011e0f812a268b4d84e19bd6'
         'ee4e0df0dbf7e80783b7aa198c67fb45'
         'fcfe0a50c69f56f66ad04127a927862e')

prepare() {
	cd ${srcdir}/${pkgbase}-$_basever
	for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
		msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
		patch -p0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
	done
	patch -p1 -i $srcdir/readline-6.3-cygwin.patch
	patch -p1 -i $srcdir/readline-6.3-msys2.patch

	# patch from upstream mailing list to fix vi mode
	patch -p2 -i $srcdir/readline-6.3-vi-last.patch

	# patch from upstream mailing list to fix the crash after pressing Delete twice
	patch -p2 -i $srcdir/readline-6.3-dispatch-multikey.patch
}

build() {
	cd ${srcdir}/${pkgbase}-$_basever

	# Remove RPATH from shared objects (FS#14366)
	sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

	./configure \
		--prefix=/usr \
		--build=${CHOST} \
		--host=${CHOST} \
		--with-curses \
		bash_cv_termcap_lib=libncurses
	make VERBOSE=1

	make DESTDIR=$srcdir/dest install
}

package_libreadline() {
	mkdir -p ${pkgdir}/usr
	cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
	cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_libreadline-devel() {
	groups=('development')
	options=('!staticlibs')
	depends=("libreadline=${pkgver}")
	pkgdesc="readline headers and libraries"

	mkdir -p ${pkgdir}/usr
	cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
	cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
