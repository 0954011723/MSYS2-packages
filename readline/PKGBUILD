# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgbase=readline
pkgname=('libreadline' 'libreadline-devel')
_basever=6.2
_patchlevel=004 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library"
arch=('i686' 'x86_64')
groups=('libraries')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('ncurses')
makedepends=('ncurses-develop')
#backup=('etc/inputrc')
options=('!emptydirs')
install=readline.install
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        readline-6.2-msys2.patch)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi
md5sums=('67948acb2ca081f23359d0256e9a271c'
         'SKIP'
         '0e3392c9789a9cd8284218d515fa8f03'
         '83287d52a482f790dfb30ec0a8746669'
         'SKIP'
         '0665020ea118e8434bd145fb71f452cc'
         'SKIP'
         'c9d5d79718856e711667dede87cb7622'
         'SKIP'
         'c08e787f50579ce301075c523fa660a4'
         'SKIP')

prepare() {
  cd ${srcdir}/${pkgbase}-$_basever
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -p0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done
  patch -p1 -i $srcdir/readline-6.2-msys2.patch
}

build() {
  cd ${srcdir}/${pkgbase}-$_basever

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure --prefix=/usr --with-curses bash_cv_termcap_lib=libncurses
  make
  
  make DESTDIR=$srcdir/dest install
}

package_libreadline() {
  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/bin ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/share ${pkgdir}/usr/
}

package_libreadline-devel() {
  groups=('development')
  options=('!staticlibs')
  depends=("libreadline=${pkgver}")
  pkgdesc="readline headers and libraries"
  
  mkdir -p ${pkgdir}/usr
  cp -rf $srcdir/dest/usr/include ${pkgdir}/usr/
  cp -rf $srcdir/dest/usr/lib ${pkgdir}/usr/
}
