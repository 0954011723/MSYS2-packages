# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=wget
pkgver=1.15
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
groups=('base-devel')
depends=('gcc-libs' 'openssl' 'libiconv' 'libidn' 'libintl' 'libpcre' 'zlib')
checkdepends=('perl-HTTP-Daemon' 'perl-IO-Socket-SSL')
optdepends=('ca-certificates: HTTPS downloads')
backup=('usr/etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        1.13.4-sysconfdir.patch
        wget-1.14-msys2.patch)
sha1sums=('e9fb1d25fa04f9c69e74e656a3174dca02700ba1'
          'SKIP'
          'da9a76951c305b456931c7ed462c12b99fd94d79'
          'ffdba68b87e1ffd59a6bd71075c6f554d0d13d3e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p2 -i "${srcdir}/1.13.4-sysconfdir.patch"
  patch -p1 -i "${srcdir}/wget-1.14-msys2.patch"
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/usr/ssl/certs/ca-bundle.crt
EOF
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --build=${CHOST} \
  --prefix=/usr \
  --with-ssl=openssl \
  --without-libiconv-prefix \
  --without-libintl-prefix \
  --without-libpth-prefix \
  --without-libssl-prefix \
  --without-libgnutls-prefix
  #ac_cv_header_spawn_h=no
  
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
