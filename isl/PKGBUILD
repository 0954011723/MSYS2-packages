# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('isl' 'isl-devel')
pkgver=0.12.1
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://freecode.com/projects/isl"
groups=('libraries')
depends=('gmp')
makedepends=('gmp-devel')
license=('MIT')
source=(http://isl.gforge.inria.fr/${pkgname}-$pkgver.tar.bz2
#ftp://ftp.linux.student.kuleuven.be/pub/people/skimo/isl/${pkgname}-$pkgver.tar.bz2
        isl-0.12.1-msys2.patch
        isl-0.12.1-no-undefined.patch)
md5sums=('27d381085ff1b5733c3e8a9b6ad24a4d'
         'a12231f05ae62cbc278ebf0e253c10d8'
         'ca9a6f7dd457cfa1cd33597d23046fe9')

prepare() {
  cd "$srcdir/${pkgname}-$pkgver"
  patch -p1 -i ${srcdir}/isl-0.12.1-msys2.patch
  patch -p1 -i ${srcdir}/isl-0.12.1-no-undefined.patch
}

build() {
  cd "$srcdir/${pkgname}-$pkgver"
  ./configure --prefix=/usr --enable-shared --disable-static
  make
  make -j1 DESTDIR="$srcdir/dest" install
}

check() {
  cd "$srcdir/${pkgname}-$pkgver"
  make check
}

package_isl() {
  mkdir -p $pkgdir/usr/bin

  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin

  install -Dm644 $srcdir/${pkgname}-$pkgver/LICENSE $pkgdir/usr/share/licenses/isl/LICENSE
}

package_isl-devel() {
  pkgdesc="ISL headers and libraries"
  groups=('development')
  depends=("isl=${pkgver}" 'gmp-devel')

  mkdir -p $pkgdir/usr

  cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}
