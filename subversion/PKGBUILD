# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=subversion
pkgver=1.8.8
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
url="http://subversion.apache.org/"
license=('APACHE')
groups=('VCS')
depends=('libsqlite' 'file' 'libserf' 'libsasl')
makedepends=('python2' 'perl' 'swig' 'ruby')
optdepends=('bash-completion: for svn bash completion'
            'python2: for some hook scripts'
            'ruby: for some hook scripts')
provides=('svn')
options=('!makeflags' '!libtool' '!emptydirs')
noextract=("subversion-${pkgver}.tar.bz2")
source=(http://www.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        svnclean
		01-make-build-directories.patch
		02-fix-linking.patch
		03-fix-linking-perl.patch
		04-fix-switch-tests.patch
		05-retry-loop.patch
		06-svnadmin-binary-mode.patch
		07-fix-path-tests.patch
		08-ruby-test-fix.patch
		09-wincrypt.patch
		10-perl-vendors.patch
		11-XFAIL-case-sensitive-move-tests.patch
		12-fix-svnlook-test.patch
		14-dso_open.patch
		14-fix_svnauthz_tests.patch
		15-XFAIL_case_sensitive_tree_conflict.patch
		16-XFAIL_sqlite3_temp_table_tests.patch
		subversion-1.8.0-msys2.patch
		remove-checking-symlink.patch)
md5sums=('db04a587c68e38da6d0e342e6508f514'
         'SKIP'
         '07ee0cb01bdf8468cb8d4c22c0d6bc04'
         '42faeb019c65be928a90d0cf4203d65c'
         '0a858a775000407676900d23d9e717ac'
         'ff627c4baa06c04fbccfd2e06189ab5f'
         '2ba451721e676ea689d3538b9c436479'
         '72a774fab01ea2f5c83f4d68b0dea0ab'
         '3a45dc619d830e6efef90d8ef2782923'
         'f307b3c8d0ffe2967aa30cdbc6d22b0a'
         '232c54625c9c4fd4f060d7b735da0315'
         '66e52cdefc037c05163fc89a289b2578'
         '2b54f0bc7ebb43d933d57ed565fd02cc'
         '69ef6bf65f26916a29f80fcabe480491'
         '8c73463534e1005cd0a8e562f7de69c9'
         '8baab01eb6847d162abe3d43a9ed07e2'
         'ca7fb78bcd33a12845168e603128aa5d'
         '19ebc3da4d7735c86195e5c0907ff3cf'
         '7bde0022868c254023aaca3f4dd671b2'
         '44d3666035747d6fdadb1dff120d56d7'
         'be151d5feeab94774c1366382044f821')

PYTHON_SITELIB=/usr/lib/python2.7/site-packages

prepare() {
   cd $startdir/
   [ -d $srcdir/$pkgname-$pkgver ] || tar -xjvf ${pkgname}-${pkgver}.tar.bz2 -C $srcdir || true
   
   cd $srcdir/$pkgname-$pkgver

   sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
       tools/hook-scripts/{,mailer/{,tests/}}*.py \
	   tools/client-side/mergeinfo-sanitizer.py 

   sed -i 's|/usr/bin/env python|/usr/bin/env python2|' gen-make.py build/getversion.py
   sed -i 's|except IOError,|except IOError as|' build/getversion.py
   
   patch -p1 -i ${srcdir}/01-make-build-directories.patch
   patch -p1 -i ${srcdir}/02-fix-linking.patch
   patch -p1 -i ${srcdir}/03-fix-linking-perl.patch
   patch -p1 -i ${srcdir}/04-fix-switch-tests.patch
   patch -p1 -i ${srcdir}/05-retry-loop.patch
   patch -p1 -i ${srcdir}/06-svnadmin-binary-mode.patch
   patch -p1 -i ${srcdir}/07-fix-path-tests.patch
   patch -p1 -i ${srcdir}/08-ruby-test-fix.patch
   patch -p1 -i ${srcdir}/09-wincrypt.patch
   patch -p1 -i ${srcdir}/10-perl-vendors.patch
   patch -p1 -i ${srcdir}/11-XFAIL-case-sensitive-move-tests.patch
   patch -p1 -i ${srcdir}/12-fix-svnlook-test.patch
   patch -p1 -i ${srcdir}/14-dso_open.patch
   patch -p1 -i ${srcdir}/14-fix_svnauthz_tests.patch
   patch -p1 -i ${srcdir}/15-XFAIL_case_sensitive_tree_conflict.patch
   patch -p1 -i ${srcdir}/16-XFAIL_sqlite3_temp_table_tests.patch
   patch -p1 -i ${srcdir}/subversion-1.8.0-msys2.patch
   patch -p1 -i ${srcdir}/remove-checking-symlink.patch
   

   PYTHON=/usr/bin/python2 ./autogen.sh
}

build() {
   cd ${pkgname}-${pkgver}
   export PYTHON=/usr/bin/python2
   ./configure --build=${CHOST} --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-serf=/usr --without-apxs \
               --with-sqlite=/usr \
               --without-gnome-keyring \
               --enable-shared --disable-static \
               --with-ruby-sitedir=/usr/lib/ruby/vendor_ruby \
			   --enable-disallowing-of-undefined-references \
			   --enable-local-library-preloading \

   #make LT_LDFLAGS="-L$Fdestdir/usr/lib"
  # make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
   #  swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl swig-rb
	 
	 
	make clean-swig
	make autogen-swig
	make all

	plain "Compiling swig bindings: ruby"
	make swig-rb

	plain "Compiling swig bindings: perl"
	make swig-pl

	plain "Compiling swig bindings: python"  
	make swig-py swig_pydir=${PYTHON_SITELIB}/libsvn swig_pydir_extra=${PYTHON_SITELIB}/svn
}

#check() {
#   cd ${pkgname}-${pkgver}
#   export LANG=C LC_ALL=C
#   make check check-swig-pl check-swig-py check-swig-rb CLEANUP=yes # check-javahl
#}

package() {
   cd ${pkgname}-${pkgver}

   make DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=${PYTHON_SITELIB}/libsvn \
     swig_pydir_extra=${PYTHON_SITELIB}/svn \
     install install-tools install-swig-py install-swig-pl install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   ## svnclean
   install -Dm 755 ${srcdir}/svnclean "${pkgdir}"/usr/bin/svnclean

   install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
   
   # Remove illegal files
   find ${pkgdir}/usr -type f -name "*::*" | xargs rm -f
   
   # fix permissons
   find ${pkgdir}/usr -type f \( -name *.dll -o -name *.exe \) | xargs chmod 0755
}
