# Maintainer: Martell Malone <martellmalone@gmail.com>

pkgname="crosstool-ng"
pkgver=1.21.0
pkgrel=1
pkgdesc="A cross-platform open-source toolchain system"
arch=('i686' 'x86_64')
url="http://www.crosstool-ng.org/"
license=("MIT")
makedepends=("git" "gcc" "autoconf" "gperf" "binutils" "bison" "flex" "wget" "tar" "patch" "make" "texinfo" "libtool" "automake" "xz")
depends=("ncurses-devel")
options=('staticlibs' 'strip')

source=("http://crosstool-ng.org/download/crosstool-ng/crosstool-ng-${pkgver}.tar.xz"
        "0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
        "0002-use-wide-versions-of-libs-for-nconf.patch")

md5sums=('ee969dff8783605cd4acf4840113a119'
         'a893266875be8b7286e407a0df53152a'
         '06d477dab83ec76ae2532e1da3a9c529')

prepare() {
  cd "$srcdir/$pkgname-${pkgver}"
  patch -p1 < "$srcdir/0001-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
  patch -p1 < "$srcdir/0002-use-wide-versions-of-libs-for-nconf.patch"

  ./bootstrap
}

build() {
  cd "$srcdir/$pkgname-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver}"
  make DESTDIR=${pkgdir} install
}
