# Maintainer: Martell Malone <martell malone at g mail dot com>

_realname=crosstool-ng
pkgname="${_realname}-git"
_ver_base=1.20
pkgver=1.20.3000.2ba44bb
pkgrel=1
pkgdesc="A cross-platform open-source toolchain system"
arch=('i686' 'x86_64')
url="http://www.crosstool-ng.org/"
license=("MIT")
makedepends=("git" "gcc" "autoconf" "gperf" "bison" "flex" "wget" "tar" "patch" "make" "libtool" "automake" "xz")
provides=("${_realname}")
conflicts=("${_realname}")
depends=( "ncurses-devel" )
options=('staticlibs' 'strip')

source=("${_realname}"::"git+https://github.com/crosstool-ng/crosstool-ng.git"
    "0001-functions-Add-CT_FindRelativePath.patch"
    "0002-Use-CT_FindRelativePath-for-gcc-and-gmp.patch"
    "0003-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
    "0004-AC_SEARCH_LIBS-more-libs-for-build-on-MSYS2-Cygwin-M.patch"
    "0005-Change-both-fs-case-sensitivity-Aborts-to-Log-Warns.patch"
    "0006-CT_DoForceRmdir-added-support-for-MSYS-and-MINGW.patch")

md5sums=('SKIP'
         'efcb94aa0f7f8d03972381eb91e7b22c'
         '3eb2d702dcc113b78a25f1ea44253a7c'
         '17e3292a692e6861566e4341ab0d8867'
         '72b9599a5c0f551d3099f63d45fb45a6'
         '710690153971c33311f6a7b8397e7a20'
         '2962c8e7f46f590ba9df1e971e36fd37')

pkgver() {
  cd "$srcdir/$_realname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {

  cd "$srcdir/$_realname"

  git am "$srcdir/0001-functions-Add-CT_FindRelativePath.patch"
  git am "$srcdir/0002-Use-CT_FindRelativePath-for-gcc-and-gmp.patch"
  git am "$srcdir/0003-Modify-config-to-support-correct-Mingw64-Triplet-Nam.patch"
  git am "$srcdir/0004-AC_SEARCH_LIBS-more-libs-for-build-on-MSYS2-Cygwin-M.patch"
  git am "$srcdir/0005-Change-both-fs-case-sensitivity-Aborts-to-Log-Warns.patch"
  git am "$srcdir/0006-CT_DoForceRmdir-added-support-for-MSYS-and-MINGW.patch"
}

build() {
  cd "$srcdir/$_realname"
  ./bootstrap
  ./configure --build=${CHOST} \
    --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_realname"
  make DESTDIR=${pkgdir} install
}
