# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=intltool
pkgver=0.50.2
pkgrel=2
pkgdesc="The internationalization tool collection"
license=('GPL3')
arch=('i686' 'x86_64')
url="https://edge.launchpad.net/intltool"
groups=('base-devel')
makedepends=('perl')
depends=('perl-XML-Parser')
source=(http://edge.launchpad.net/intltool/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz{,.asc}
        localedir-fix.patch)
sha1sums=('7fddbd8e1bf94adbf1bc947cbf3b8ddc2453f8ad'
          'SKIP'
          '40619031188a35c96198eaf644ca85e00eb3edca')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 -i ${srcdir}/localedir-fix.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 doc/I18N-HOWTO "${pkgdir}/usr/share/doc/${pkgname}/I18N-HOWTO"
}
