# Maintainer: Martell Malone <martellmalone@gmail.com>

pkgname=inetutils
pkgver=1.9.2
pkgrel=1
pkgdesc="A collection of common network programs."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL3')
groups=('base')
makedepends=('automake' 'autoconf' 'pkg-config' 'make')
options=('!emptydirs')
install=inetutils.install
source=(http://ftp.gnu.org/gnu/inetutils/${pkgname}-${pkgver}.tar.xz)
sha1sums=('f9fc5c3ba7046d95fdfd0f1c6adf508220082893')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --libexec=/usr/bin \
    --localstatedir=/var --sysconfdir=/etc \
    --without-wrap --without-pam \
    --enable-ftp --enable-ftpd \
    --enable-telnet --enable-telnetd \
    --enable-talk --enable-talkd \
    --enable-rlogin --enable-rlogind \
    --enable-rsh --enable-rshd \
    --enable-rcp --enable-hostname \
    --enable-dnsdomainname \
    --disable-rexec --disable-rexecd \
    --disable-tftp --disable-tftpd \
    --disable-ping --disable-ping6 \
    --disable-logger --disable-syslogd \
    --disable-inetd --disable-whois \
    --disable-uucpd --disable-ifconfig \
    --disable-traceroute
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
