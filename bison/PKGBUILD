# Maintainer: Alexey Pavlov <alexpux@gmail.com

pkgname=bison
pkgver=3.0
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('m4' 'sh')
groups=('base-devel')
options=('staticlibs')
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz{,.sig}
        bison-3.0-cygwin-fpending.patch
        bison-3.0-msys2.patch)
md5sums=('a2624994561aa69f056c904c1ccb2880'
         'SKIP'
         '7c2f901e8f74306bad9eec38d4b371ab'
         '2f11ef7ba8fb3938d4aa6a3158ee7238')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/bison-3.0-cygwin-fpending.patch
  patch -p1 -i ${srcdir}/bison-3.0-msys2.patch
  
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
     ac_cv_header_spawn_h=no
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
