# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gcc
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
pkgver=4.9.1
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("zlib" "mpc" "cloog"
         "${_mingw_suff}-binutils"
         "${_mingw_suff}-crt"
         "${_mingw_suff}-headers"
         "${_mingw_suff}-winpthreads"
         "${_mingw_suff}-windows-default-manifest")
makedepends=("gcc" 'lndir' "gmp-devel" "mpc-devel" "cloog-devel" "zlib-devel" "isl-devel")
#checkdepends=('dejagnu')
optdepends=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("ftp://gcc.gnu.org/pub/gcc/releases/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2"
        4.5-java-FIONREAD.patch
        4.5-skiptest.patch
        4.7-ada.patch
        4.7-boehm-gc-cygwin.patch
        4.7-execstack.patch
        4.7-java-awt.patch
        4.7-java-jni.patch
        4.7-libffi-noinst.patch
        4.7-libitm-cygwin.patch
        4.8-gcc-ada-DTR.patch
        4.8-java-soname.patch
        4.8-libatomic-cygwin.patch
        4.8-libffi-cygwin64.patch
        4.8-lto-plugin-soname.patch
        4.8-specs-cygwin.patch
        4.8-Wimplicit-decls.patch
        4.8-libstdc++export.patch
        4.8-filename-output.patch
        4.8-pr-57440.patch
        4.9-prettify-linking-no-undefined.mingw.patch
        4.9.1-PR-c-61214.patch
        gcc-4.8.0-configure-msys2.patch
        gcc-4.8.0-libgcc-msys2.patch
        gcc-4.8.0-msys2-spec.patch)
md5sums=('fddf71348546af523353bd43d34919c1'
         '741e079f5ec80aee1a552d0ec0b74575'
         '24c9520471f62669b4bc0e0a939547fa'
         'abfde62661674ecd867fb66cff6d3ded'
         '3e56bc1fac8b3b6d0bb2af0a380622e1'
         'f7cb43c6a6a54e55027e70a9b03774b2'
         '01ef40c1f5b0cc4cdbc5be94008743e8'
         '1887614712a7cd79f771927adf78e760'
         '15da8d111f8eab15597db353537aefd3'
         'da668f9ac5a280fadebc2a2cd37896e0'
         'f3302086c3246e1fc45cac73deb9429d'
         '6dd3e475475a14b5a767a1e42d57009b'
         '9668346a30879e0d00355774e04881e5'
         '2ab5cde66d656ec85713ef4dcb470a74'
         '711c3e3bfad64560808efcef37a43a5e'
         '92b4c654a7951e1026d103bd5c0ddcca'
         'eb1c05fc48755cdf685d871ae49b1eef'
         'f6b5fb08ff06a7d60ee3280e14240bb2'
         '40cb437805e2f7a006aa0d0c3098ab0f'
         '347336d5a318e5e8b3cde29e9174413c'
         '09bdad41515c8d353612f30c91b3bac2'
         '7b7021f61fbe883962458255cf2d4fea'
         '0cad08f12b27fab33264de04bb3a58b1'
         '8939d9e1a18a1181bbb95da8efb8e98f'
         '0d85279666763cbc20b5aa15219f195a')

_threads="win32"
_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
  
  # Cygwin patches
  patch -p2 -i ${srcdir}/4.7-ada.patch
  patch -p2 -i ${srcdir}/4.7-boehm-gc-cygwin.patch
  patch -p2 -i ${srcdir}/4.7-execstack.patch
  patch -p2 -i ${srcdir}/4.7-java-awt.patch
  patch -p2 -i ${srcdir}/4.5-java-FIONREAD.patch
  patch -p2 -i ${srcdir}/4.7-java-jni.patch
  patch -p0 -i ${srcdir}/4.8-java-soname.patch
  patch -p2 -i ${srcdir}/4.8-libatomic-cygwin.patch
  patch -p2 -i ${srcdir}/4.7-libffi-noinst.patch
  patch -p2 -i ${srcdir}/4.8-libffi-cygwin64.patch
  patch -p2 -i ${srcdir}/4.7-libitm-cygwin.patch
  patch -p2 -i ${srcdir}/4.8-lto-plugin-soname.patch
  patch -p0 -i ${srcdir}/4.8-specs-cygwin.patch
  patch -p2 -i ${srcdir}/4.5-skiptest.patch
  
  patch -p2 -i ${srcdir}/4.8-Wimplicit-decls.patch

  # Upstream patches
   patch -p1 -i ${srcdir}/4.9.1-PR-c-61214.patch

  #MSYS2 Patches
  patch -p1 -i ${srcdir}/gcc-4.8.0-configure-msys2.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-msys2-spec.patch
  patch -p1 -i ${srcdir}/gcc-4.8.0-libgcc-msys2.patch
  
  # Misc patches
  patch -p0 -i ${srcdir}/4.8-filename-output.patch
  patch -p1 -i ${srcdir}/4.8-libstdc++export.patch
  patch -p1 -i ${srcdir}/4.9-prettify-linking-no-undefined.mingw.patch
  patch -p1 -i ${srcdir}/4.8-pr-57440.patch
}

build() {
  for _target in ${_targets}; do
  mkdir -p ${srcdir}/gcc-build-${_target} && cd ${srcdir}/gcc-build-${_target}
    
  case "${_target}" in
    i686*)
      local _conf="--disable-sjlj-exceptions --with-dwarf2"
      local _arch=i686
    ;;
    
    x86_64*)
      local _arch=x86-64
      local _conf=""
    ;;
  esac

  ../${_realname}-${pkgver}/configure \
    --prefix=/opt \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${_target} \
    --libexecdir=/opt/lib \
    --disable-bootstrap \
    --with-arch=${_arch} \
    --with-tune=generic \
    --enable-languages=c,lto,c++,fortran \
    --enable-shared --enable-static \
    --enable-threads=${_threads} \
    --enable-graphite \
    --enable-fully-dynamic-string \
    --enable-libstdcxx-time=yes \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --enable-cloog-backend=isl \
    --enable-version-specific-runtime-libs \
    --disable-cloog-version-check --disable-isl-version-check \
    --enable-lto \
    --enable-libgomp \
    --disable-multilib \
    --enable-checking=release \
    --disable-rpath \
    --disable-win32-registry \
    --disable-werror \
    --disable-symvers \
    --with-libiconv \
    --with-system-zlib \
    --with-{gmp,mpfr,mpc,isl,cloog}=/usr \
    --with-gnu-as --with-gnu-ld \
    ${_conf} \
    CFLAGS="-O2 -pipe" \
    CXXFALGS="-O2 -pipe"

  make all
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/gcc-build-${_target}
    make -j1 DESTDIR=${pkgdir} install
    mv ${pkgdir}/opt/lib/gcc/${_target}/lib/libgcc_s.a ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/
    mv ${pkgdir}/opt/lib/gcc/${_target}/libgcc*.dll ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/
    for _f in ${pkgdir}/opt/bin/*.exe; do
      strip -s ${_f}
    done
    for _f in ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/*.exe; do
      strip -s ${_f}
    done    
    rm -rf ${pkgdir}/opt/share
  done
}
