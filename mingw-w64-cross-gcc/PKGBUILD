# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gcc
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
pkgver=4.9.2
pkgrel=1
pkgdesc="Cross GCC for the MinGW-w64"
arch=('i686' 'x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("zlib" "mpc" "cloog"
         "${_mingw_suff}-binutils"
         "${_mingw_suff}-crt"
         "${_mingw_suff}-headers"
         "${_mingw_suff}-winpthreads"
         "${_mingw_suff}-windows-default-manifest")
makedepends=("gcc" 'lndir' "gmp-devel" "mpc-devel" "cloog-devel" "zlib-devel" "isl-devel")
#checkdepends=('dejagnu')
optdepends=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("ftp://gcc.gnu.org/pub/gcc/releases/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2"
        001-4.7-ada.patch
        005-4.7-boehm-gc-cygwin.patch
        010-4.7-execstack.patch
        015-4.7-java-awt.patch
        016-4.5-java-FIONREAD.patch
        017-4.7-java-jni.patch
        018-4.8-java-soname.patch
        025-4.8-libatomic-cygwin.patch
        030-4.7-libffi-noinst.patch
        031-4.8-libffi-cygwin64.patch
        035-4.7-libitm-cygwin.patch
        040-4.8-lto-plugin-soname.patch
        045-4.8-specs-cygwin.patch
        046-4.8-__cxa_atexit.patch
        047-4.8-libstdc-atexit_thread.patch
        048-4.8-medium-model-dllimport.patch
        050-4.5-skiptest.patch
        055-4.8-gcc-ada-DTR.patch
        060-4.8-Wimplicit-decls.patch
        065-4.9-prettify-linking-no-undefined.mingw.patch
        070-4.8-filename-output.patch
        075-4.8-pr-57440.patch
        080-4.8-libstdc++export.patch
        950-4.8.0-configure-msys2.patch
        951-4.8.0-msys2-spec.patch
        952-4.8.0-libgcc-msys2.patch
        953-4.9.1-testsuite-msys2.patch)
md5sums=('4df8ee253b7f3863ad0b86359cd39c43'
         '62c46773400b16f693d7b89f43c00433'
         '170b28d7f480eec4b4721d503d96eab0'
         'b3700e51b0365894ce5520e8f5bbeeb7'
         '10ef60468b9718c59040db592b8419ed'
         '336af37d59e0e468e5c2cac96b476870'
         'b1e8f894e9091a7f43f84bc2b78ecdf8'
         '5e9a26f08a755ede27420207c79addac'
         '0b3ec52ce6c02d493eeb1a51786383e9'
         'd9ea16697abfb3a8903f75e056939855'
         '43dfb4455539fc60721724ba6e1349af'
         'dc4ecfa1b1caa272c845d55360cdffe3'
         'a42858255e308a6922d5e8d36f5b3c06'
         '3506054ca98d8a21fd36605b0b863533'
         '07be14230e64a003d9095658df7e4c91'
         '3ce9a2de6fae8be2ed1d477783f18056'
         '92f89009064abb5e98e59bb44de252df'
         'c52b3fd6949751f4dc43f10d6f5ec751'
         '5ac0fc6a780aee8583b89d7225a85f34'
         'ee07cb9166090f95c0f810de0667ea3d'
         '09bdad41515c8d353612f30c91b3bac2'
         'b05b64cd00dda6b14d22a98c68f7eefe'
         '347336d5a318e5e8b3cde29e9174413c'
         'f6b5fb08ff06a7d60ee3280e14240bb2'
         '6c1e48e2723cf1df7f9c826722fda193'
         'd27c17a4c6b9caadc281a4f8e204b2eb'
         '7df48a22637ae227ed91fec7d43667fd'
         'a0132606c8f1f2644281a208b3e69a0f')

_threads="win32"
_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure
  
  #Cygwin patches

  patch -p1 -i ${srcdir}/001-4.7-ada.patch
  patch -p1 -i ${srcdir}/005-4.7-boehm-gc-cygwin.patch
  patch -p1 -i ${srcdir}/010-4.7-execstack.patch
  patch -p1 -i ${srcdir}/015-4.7-java-awt.patch
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=35536
  patch -p1 -i ${srcdir}/016-4.5-java-FIONREAD.patch
  patch -p1 -i ${srcdir}/017-4.7-java-jni.patch
  patch -p1 -i ${srcdir}/018-4.8-java-soname.patch
  patch -p1 -i ${srcdir}/025-4.8-libatomic-cygwin.patch
  patch -p1 -i ${srcdir}/030-4.7-libffi-noinst.patch
  patch -p1 -i ${srcdir}/031-4.8-libffi-cygwin64.patch
  patch -p1 -i ${srcdir}/035-4.7-libitm-cygwin.patch
  patch -p1 -i ${srcdir}/040-4.8-lto-plugin-soname.patch
  patch -p1 -i ${srcdir}/045-4.8-specs-cygwin.patch
  patch -p1 -i ${srcdir}/046-4.8-__cxa_atexit.patch
  patch -p1 -i ${srcdir}/047-4.8-libstdc-atexit_thread.patch
  patch -p1 -i ${srcdir}/048-4.8-medium-model-dllimport.patch
  patch -p1 -i ${srcdir}/050-4.5-skiptest.patch

  if [ "$CARCH" = "i686" ]
  then
    patch -p1 -i ${srcdir}/055-4.8-gcc-ada-DTR.patch
  fi

  patch -p1 -i ${srcdir}/060-4.8-Wimplicit-decls.patch

  # Misc patches
  patch -p1 -i ${srcdir}/065-4.9-prettify-linking-no-undefined.mingw.patch
  patch -p1 -i ${srcdir}/070-4.8-filename-output.patch
  patch -p1 -i ${srcdir}/075-4.8-pr-57440.patch
  patch -p1 -i ${srcdir}/080-4.8-libstdc++export.patch
  
  #MSYS2 Patches
  patch -p1 -i ${srcdir}/950-4.8.0-configure-msys2.patch
  patch -p1 -i ${srcdir}/951-4.8.0-msys2-spec.patch
  patch -p1 -i ${srcdir}/952-4.8.0-libgcc-msys2.patch
  patch -p1 -i ${srcdir}/953-4.9.1-testsuite-msys2.patch
}

build() {
  for _target in ${_targets}; do
  mkdir -p ${srcdir}/gcc-build-${_target} && cd ${srcdir}/gcc-build-${_target}
    
  case "${_target}" in
    i686*)
      local _conf="--disable-sjlj-exceptions --with-dwarf2"
      local _arch=i686
    ;;
    
    x86_64*)
      local _arch=x86-64
      local _conf=""
    ;;
  esac

  ../${_realname}-${pkgver}/configure \
    --prefix=/opt \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${_target} \
    --libexecdir=/opt/lib \
    --disable-bootstrap \
    --with-arch=${_arch} \
    --with-tune=generic \
    --enable-languages=c,lto,c++,fortran \
    --enable-shared --enable-static \
    --enable-threads=${_threads} \
    --enable-graphite \
    --enable-fully-dynamic-string \
    --enable-libstdcxx-time=yes \
    --disable-libstdcxx-pch \
    --disable-libstdcxx-debug \
    --enable-cloog-backend=isl \
    --enable-version-specific-runtime-libs \
    --disable-cloog-version-check --disable-isl-version-check \
    --enable-lto \
    --enable-libgomp \
    --disable-multilib \
    --enable-checking=release \
    --disable-rpath \
    --disable-win32-registry \
    --disable-werror \
    --disable-symvers \
    --with-libiconv \
    --with-system-zlib \
    --with-{gmp,mpfr,mpc,isl,cloog}=/usr \
    --with-gnu-as --with-gnu-ld \
    ${_conf} \
    CFLAGS="-O2 -pipe" \
    CXXFALGS="-O2 -pipe"

  make all
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/gcc-build-${_target}
    make -j1 DESTDIR=${pkgdir} install
    mv ${pkgdir}/opt/lib/gcc/${_target}/lib/libgcc_s.a ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/
    mv ${pkgdir}/opt/lib/gcc/${_target}/libgcc*.dll ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/
    for _f in ${pkgdir}/opt/bin/*.exe; do
      strip -s ${_f}
    done
    for _f in ${pkgdir}/opt/lib/gcc/${_target}/${pkgver}/*.exe; do
      strip -s ${_f}
    done    
    rm -rf ${pkgdir}/opt/share
  done
}
