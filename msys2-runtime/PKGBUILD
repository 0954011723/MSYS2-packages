# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=('msys2-runtime' 'msys2-runtime-devel')
_ver_base=2.0
pkgver=2.0.15837.1ae5eaf
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base')
depends=()
makedepends=('cocom' 'git' 'perl' 'gcc' 'mingw-w64-cross-gcc' 'mingw-w64-cross-zlib')
# options=('debug' '!strip')
source=('msys2-runtime'::'git+https://github.com/Alexpux/Cygwin.git#branch=develop'
        '0001-Expand-CYGWIN-error_start-processing.patch')
md5sums=('SKIP'
         '85fabbc5d9cdb46cf73da9654f2c83bc')

pkgver() {
	cd "$srcdir/msys2-runtime"
	#printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/msys2-runtime"
	patch -p1 -i ${srcdir}/0001-Expand-CYGWIN-error_start-processing.patch
}

build() {
	[[ -d $srcdir/build-${CHOST} ]] && rm -rf $srcdir/build-${CHOST}
	mkdir -p $srcdir/build-${CHOST} && cd $srcdir/build-${CHOST}

	OPTIM="-O2 -DNDEBUG"
	if check_option "debug" "y"; then
		OPTIM="-O0 -DDEBUG"
	fi

	CFLAGS="$OPTIM -pipe -ggdb -fdebug-prefix-map=$srcdir/build-${CHOST}=/usr/src/debug/msys -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
	CXX=g++
	CXXFLAGS="$OPTIM -pipe -ggdb -fdebug-prefix-map=$srcdir/build-${CHOST}=/usr/src/debug/msys -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
	F77=gfortran
	FFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/build-${CHOST}=/usr/src/debug/msys -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
	FC=gfortran
	FCFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/build-${CHOST}=/usr/src/debug/msys -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"

	# Calling conigure via an absolute path makes debugging easier as that way absolute paths end up the debugging info.
	$srcdir/msys2-runtime/configure \
		--prefix=/usr \
		--build=${CHOST} \
		--sysconfdir=/etc
	LANG=C make
	LANG=C make -j1 DESTDIR=${srcdir}/dest install
	rm -rf ${srcdir}/dest/etc
}

package_msys2-runtime() {
	pkgdesc="Posix emulation engine for Windows"
	groups=('base')
	options=('!strip')
	#install=msys2-runtime.install

	mkdir -p $pkgdir/usr
	cp -rf ${srcdir}/dest/usr/bin $pkgdir/usr/
	rm -f $pkgdir/usr/bin/msys-2.0.dbg
	rm -f $pkgdir/usr/bin/cyglsa-config
	rm -f $pkgdir/usr/bin/cyglsa.dll
	rm -f $pkgdir/usr/bin/cyglsa64.dll
	rm -f $pkgdir/usr/bin/cygserver-config  
	cp -rf ${srcdir}/dest/usr/share $pkgdir/usr/
}

package_msys2-runtime-devel() {
	pkgdesc="MSYS2 headers and libraries"
	groups=('MSYS2-devel')
	depends=("msys2-runtime=${pkgver}")
	options=('staticlibs' '!strip')

	mkdir -p $pkgdir/usr/bin
	cp -f ${srcdir}/dest/usr/bin/msys-2.0.dbg $pkgdir/usr/bin/
	cp -rf ${srcdir}/dest/usr/${CHOST}/include $pkgdir/usr/
	rm -f $pkgdir/usr/include/iconv.h
	rm -f $pkgdir/usr/include/unctrl.h
	cp -rf ${srcdir}/dest/usr/${CHOST}/lib $pkgdir/usr/
}
