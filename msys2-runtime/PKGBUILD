# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('msys2-runtime' 'msys2-runtime-devel')
_ver_base=2.0
pkgver=2.0.15707.2a2164e
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base')
depends=()
makedepends=('cocom' 'git' 'perl' 'gcc' 'mingw-w64-cross-gcc' 'mingw-w64-cross-zlib')
source=('msys2-runtime'::'git+https://github.com/Alexpux/Cygwin.git#branch=develop')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/msys2-runtime"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
build() {
  mkdir -p $srcdir/msys2-build && cd $srcdir/msys2-build

  CFLAGS="-O2 -pipe -ggdb -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
  CXX=g++
  CXXFLAGS="-O2 -pipe -ggdb -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
  F77=gfortran
  FFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"
  FC=gfortran
  FCFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/msys2-runtime=/usr/src/debug/msys"

  ../msys2-runtime/configure --prefix=/usr --build=${CHOST} --sysconfdir=/etc
  LANG=C make
  
}

setup() {
  cd $srcdir/msys2-build
  make -j1 DESTDIR="$destdir" install
  rm -rf $destdir/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p $pkgdir/usr
  cp -rf $destdir/usr/bin $pkgdir/usr/
  rm -f $pkgdir/usr/bin/msys-2.0.dbg
  rm -f $pkgdir/usr/bin/cyglsa-config
  rm -f $pkgdir/usr/bin/cyglsa.dll
  rm -f $pkgdir/usr/bin/cyglsa64.dll
  rm -f $pkgdir/usr/bin/cygserver-config  
  cp -rf $destdir/usr/share $pkgdir/usr/
  
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')
  
  mkdir -p $pkgdir/usr/bin
  cp -f $destdir/usr/bin/msys-2.0.dbg $pkgdir/usr/bin/
  cp -rf $destdir/usr/${CHOST}/include $pkgdir/usr/
  rm -f $pkgdir/usr/include/iconv.h
  rm -f $pkgdir/usr/include/unctrl.h
  cp -rf $destdir/usr/${CHOST}/lib $pkgdir/usr/
}
