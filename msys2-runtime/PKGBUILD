# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=('msys2-runtime' 'msys2-runtime-devel')
_ver_base=2.0
pkgver=2.0.15923.0cbf0b3
pkgrel=2
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base')
depends=()
makedepends=('cocom' 'git' 'perl' 'gcc' 'mingw-w64-cross-gcc' 'mingw-w64-cross-zlib')
#options=('debug' '!strip')
source=('msys2-runtime'::'git+https://github.com/Alexpux/Cygwin.git#branch=develop'
        '0001-Expand-CYGWIN-error_start-processing.patch'
        '0002-revert-mounting.patch'
        '0003-Add-msys2_path_conv.patch'
        '0004-Fix-cygwin-path_conv-of-root.patch')
md5sums=('SKIP'
         '85fabbc5d9cdb46cf73da9654f2c83bc'
         'ace414fde822ae633183fe94cbb0e1d9'
         '57a5c1bbc3796552f1e08953c7f73a75'
         '4e39847e44316ae7376323e0b7538504')
pkgver() {
	cd "$srcdir/msys2-runtime"
	#printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/msys2-runtime"
	patch -p1 -i ${srcdir}/0001-Expand-CYGWIN-error_start-processing.patch
# Experimental!
#	patch -p1 -i ${srcdir}/0002-revert-mounting.patch
# This is the new code, but it doesn't handle Unix PATH lists very well.
	patch -p1 -i ${srcdir}/0003-Add-msys2_path_conv.patch
	patch -p1 -i ${srcdir}/0004-Fix-cygwin-path_conv-of-root.patch
}

build() {
	[[ -d $srcdir/build-${CHOST} ]] && rm -rf $srcdir/build-${CHOST}
	mkdir -p $srcdir/build-${CHOST} && cd $srcdir/build-${CHOST}

	# Gives more verbose compile output when debugging.
	if check_option "debug" "y"; then
	  export CCWRAP_VERBOSE=1
	  OPTIM="-O0"
	else
	  OPTIM="-O2"
	fi

	CFLAGS="$OPTIM -pipe -ggdb"
	CXXFLAGS="$OPTIM -pipe -ggdb"

	${srcdir}/msys2-runtime/configure \
		--prefix=/usr \
		--build=${CHOST} \
		--sysconfdir=/etc
	LANG=C make
	LANG=C make -j1 DESTDIR=${srcdir}/dest install
	rm -rf ${srcdir}/dest/etc
}

package_msys2-runtime() {
	pkgdesc="Posix emulation engine for Windows"
	groups=('base')
	options=('!strip')
	#install=msys2-runtime.install

	mkdir -p $pkgdir/usr
	cp -rf ${srcdir}/dest/usr/bin $pkgdir/usr/
	rm -f $pkgdir/usr/bin/msys-2.0.dbg
	rm -f $pkgdir/usr/bin/cyglsa-config
	rm -f $pkgdir/usr/bin/cyglsa.dll
	rm -f $pkgdir/usr/bin/cyglsa64.dll
	rm -f $pkgdir/usr/bin/cygserver-config  
	cp -rf ${srcdir}/dest/usr/share $pkgdir/usr/
}

package_msys2-runtime-devel() {
	pkgdesc="MSYS2 headers and libraries"
	groups=('MSYS2-devel')
	depends=("msys2-runtime=${pkgver}")
	options=('staticlibs' '!strip')

	mkdir -p $pkgdir/usr/bin
	cp -f ${srcdir}/dest/usr/bin/msys-2.0.dbg $pkgdir/usr/bin/
	cp -rf ${srcdir}/dest/usr/${CHOST}/include $pkgdir/usr/
	rm -f $pkgdir/usr/include/iconv.h
	rm -f $pkgdir/usr/include/unctrl.h
	cp -rf ${srcdir}/dest/usr/${CHOST}/lib $pkgdir/usr/
}
