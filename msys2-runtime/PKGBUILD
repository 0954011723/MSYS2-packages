# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('msys2-runtime' 'msys2-runtime-devel')
_ver_base=2.0
pkgver=2.0.0.0ec2c05
pkgrel=1
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base')
depends=()
makedepends=('cocom' 'git')
source=('msys2-runtime'::'git+https://github.com/Alexpux/Cygwin.git#branch=develop')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
build() {
  cd $srcdir/$pkgname

  CFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/$pkgname=/usr/src/debug/msys"
  CXX=g++
  CXXFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/$pkgname=/usr/src/debug/msys"
  F77=gfortran
  FFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/$pkgname=/usr/src/debug/msys"
  FC=gfortran
  FCFLAGS+=" -ggdb -fdebug-prefix-map=$srcdir/$pkgname=/usr/src/debug/msys"

  export PATH=$PATH:/mingw32/bin:/mingw64/bin
  ./configure --prefix=/usr --build=${CHOST} --sysconfdir=/etc
  LC_ALL=en_US.UTF-8 LANG=en_US.UTF-8 make
  make DESTDIR="$srcdir/dest" install
  rm -rf $srcdir/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  #install=msys2-runtime.install

  mkdir -p $pkgdir/usr
  cp -rf $srcdir/dest/usr/bin $pkgdir/usr/
  rm -f $pkgdir/usr/bin/msys-2.0.dbg
  rm -f $pkgdir/usr/bin/cyglsa-config
  rm -f $pkgdir/usr/bin/cyglsa.dll
  rm -f $pkgdir/usr/bin/cyglsa64.dll
  rm -f $pkgdir/usr/bin/cygserver-config  
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
  
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs')
  
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/msys-2.0.dbg $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/${CHOST}/include $pkgdir/usr/
  rm -f $pkgdir/usr/include/iconv.h
  rm -f $pkgdir/usr/include/unctrl.h
  cp -rf $srcdir/dest/usr/${CHOST}/lib $pkgdir/usr/
}
