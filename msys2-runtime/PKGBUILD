# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=('msys2-runtime' 'msys2-runtime-devel')
_ver_base=2.0
pkgver=2.0.16224.6393980
pkgrel=5
pkgdesc="Cygwin POSIX emulation engine"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base')
depends=()
makedepends=('cocom'
             'git'
             'perl'
             'gcc'
             'make'
             'mingw-w64-cross-gcc'
             'mingw-w64-cross-zlib'
             'zlib-devel'
             'gettext-devel'
             'diffutils')
#options=('debug' '!strip')
source=('msys2-runtime'::'git+https://github.com/Alexpux/Cygwin.git#branch=develop'
        0001-Expand-CYGWIN-error_start-processing.patch
        0002-Replace-old-MSYS1-path-s-convert-procedure-with-newl.patch
        0003-path-Fix-cygwin-path_conv-of-root.patch
        0004-mount-Add-longest_native_sorted-and-longest_posix_so.patch
        0005-Move-need_directory-check-out-of-if-opt-PC_NOFULL-sc.patch)
md5sums=('SKIP'
         '4218e04eb6366db3fa2d84bb01588f2e'
         '228fb6ef41b3f9edc57e95510bcd194a'
         '18c9c8a85bf8b6872bef90c6b23023df'
         'd23bcd3271e0f2db1a3a7389a0446a81'
         'aafa1db2c6bc6f7e275f2a65f1b6fbde')
pkgver() {
  cd "$srcdir/msys2-runtime"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_ver_base}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/msys2-runtime"
  git am ${srcdir}/0001-Expand-CYGWIN-error_start-processing.patch
  # Experimental!
  # This is the new code, but it doesn't handle Unix PATH lists very well.
  git am ${srcdir}/0002-Replace-old-MSYS1-path-s-convert-procedure-with-newl.patch

  git am ${srcdir}/0003-path-Fix-cygwin-path_conv-of-root.patch
  git am ${srcdir}/0004-mount-Add-longest_native_sorted-and-longest_posix_so.patch
  # Also WIP
  # See failure in https://github.com/Alexpux/path_convert (once I get write access!)
  # '/' should -> 'C:/msys32/' but instead -> 'C:/msys32'
  git am ${srcdir}/0005-Move-need_directory-check-out-of-if-opt-PC_NOFULL-sc.patch
}

build() {
  [[ -d $srcdir/build-${CHOST} ]] && rm -rf $srcdir/build-${CHOST}
  mkdir -p $srcdir/build-${CHOST} && cd $srcdir/build-${CHOST}

  # Gives more verbose compile output when debugging.
  if check_option "debug" "y"; then
    export CCWRAP_VERBOSE=1
    OPTIM="-O0"
  else
    OPTIM="-O2"
  fi

  CFLAGS="$OPTIM -pipe -ggdb"
  CXXFLAGS="$OPTIM -pipe -ggdb"

  ${srcdir}/msys2-runtime/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --sysconfdir=/etc
  LANG=C make
  LANG=C make -j1 DESTDIR=${srcdir}/dest install
  rm -rf ${srcdir}/dest/etc
}

package_msys2-runtime() {
  pkgdesc="Posix emulation engine for Windows"
  groups=('base')
  options=('!strip')
  #install=msys2-runtime.install

  mkdir -p $pkgdir/usr
  cp -rf ${srcdir}/dest/usr/bin $pkgdir/usr/
  rm -f $pkgdir/usr/bin/msys-2.0.dbg
  rm -f $pkgdir/usr/bin/cyglsa-config
  rm -f $pkgdir/usr/bin/cyglsa.dll
  rm -f $pkgdir/usr/bin/cyglsa64.dll
  rm -f $pkgdir/usr/bin/cygserver-config  
  cp -rf ${srcdir}/dest/usr/share $pkgdir/usr/
}

package_msys2-runtime-devel() {
  pkgdesc="MSYS2 headers and libraries"
  groups=('MSYS2-devel')
  depends=("msys2-runtime=${pkgver}")
  options=('staticlibs' '!strip')

  mkdir -p $pkgdir/usr/bin
  cp -f ${srcdir}/dest/usr/bin/msys-2.0.dbg $pkgdir/usr/bin/
  cp -rLf ${srcdir}/dest/usr/${CHOST}/include $pkgdir/usr/
  rm -f $pkgdir/usr/include/iconv.h
  rm -f $pkgdir/usr/include/unctrl.h
  cp -rLf ${srcdir}/dest/usr/${CHOST}/lib $pkgdir/usr/
}

# return 0
# To hack on this:
# cd /e/m2/repo-MSYS2/msys2-runtime/
# pushd src/build-i686-pc-msys
# LANG=C make && make -j1 DESTDIR=/e/m2/repo-MSYS2/msys2-runtime/src/dest install
# popd
# makepkg -sRLf
# pacman -U msys2-runtime*.xz
