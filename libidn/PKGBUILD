# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libidn' 'libidn-devel')
pkgver=1.28
pkgrel=2
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
url="http://www.gnu.org/software/libidn/"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
depends=('texinfo')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libidn-1.26-msys2.patch)
install=libidn.install
sha1sums=('725587211b229c156e29fa2ad116b0ef71a7ca17'
          'a1758bd13665a735a6166bdd3ddf70cce14f4b4d')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/libidn-1.26-msys2.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR=${srcdir}/dest install
}

package_libidn() {
  groups=('libraries') 
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
}

package_libidn-devel() {
  pkgdesc="Libidn headers and libraries"
  groups=('development')
  
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.exe $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}
