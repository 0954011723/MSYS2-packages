# Maintainer: Masanori Shirayama <mashir43@gmail.com>

pkgname=tcsh
pkgver=6.18.01
pkgrel=2
epoch=
pkgdesc="C shell with file name completion and command line editing"
arch=("i686" "x86_64")
url="http://www.tcsh.org/"
license=('BSD')
groups=()
depends=('gcc-libs' 'libcatgets' 'libcrypt' 'libiconv' 'ncurses')
makedepends=('gcc' 'libcatgets-devel' 'libcrypt-devel' 'libiconv-devel' 'ncurses-devel')
provides=('csh')
conflicts=('csh')
install=tcsh.install
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"
        "msys-platform.patch")
sha256sums=('d81ca27851f3e8545666399b4bcf25433e602a195113b3f7c73886fef84c9fa8'
            'f2abc54a43c907381c1ddde3f85a9f6541329156cde7428b892fc14fad48ca12')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"
  cp -f config/cygwin config/msys
  patch -p1 -i ${srcdir}/msys-platform.patch
  autoreconf -ivf
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --build=${CHOST}
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  #make -k check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -Dm644 ./Copyright ${pkgdir}/usr/share/licenses/tcsh/Copyright
  
  cd "${pkgdir}/usr/bin"
  cp -p tcsh.exe csh.exe
}
