# Contributor (MSYS2): David Macek <david.macek.0@gmail.com>
# Maintainer (Arch Linux): Kyle Keen <keenerd@gmail.com>
# Maintainer (Arch Linux): Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor (Arch Linux): Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor (Arch Linux): arjan <arjan@archlinux.org>
# Contributor (Arch Linux): Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ucl
pkgver=1.03
pkgrel=1
pkgdesc="Portable lossless data compression library written in ANSI C"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.oberhumer.com/opensource/ucl/"
source=("http://www.oberhumer.com/opensource/$pkgname/download/$pkgname-$pkgver.tar.gz"
        "000-msys2.patch"
        "001-autoconf-compat.patch"
        "002-missing-macros.patch")
md5sums=('852bd691d8abc75b52053465846fba34'
         '130fd74b245e68acd6f005115f1412b0'
         '7b000001507b42dcc047c8786f180d9b'
         '9b596d8cec6144b6cd746efa6693e46e')

prepare() {
  cd "$pkgname-$pkgver"
  rm -f acinclude.m4
  patch -p1 -i "${srcdir}/000-msys2.patch"
  patch -p1 -i "${srcdir}/001-autoconf-compat.patch"
  patch -p1 -i "${srcdir}/002-missing-macros.patch"

  # no luck with that
  # WANT_AUTOMAKE=1.15 autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-shared \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
