# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=itstool
pkgver=2.0.0
pkgrel=1
pkgdesc="XML to PO and back again)"
arch=('i686' 'x86_64')
url="http://itstool.org/"
license=("GPL3")
depends=("python2" "libxml2" "libxml2-python")
source=(http://files.itstool.org/itstool/${pkgname}-$pkgver.tar.bz2)
sha256sums=('14708111b11b4a70e240e3b404d7a58941e61dbb5caf7e18833294d654c09169')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i '1s|#!/usr/bin/python|&2|' itstool itstool.in
	sed -i 's/| python/&2/' configure.ac
	autoreconf -fi
}

build() {
	mkdir -p "${srcdir}/build-${CHOST}"
	cd "${srcdir}/build-${CHOST}"
    ../${pkgname}-${pkgver}/configure \
		--prefix=/usr \
		--build=${CHOST} \
		--host=${CHOST} \
		--target=${CHOST}
    make
}

check() {
  cd build-${CHOST}
  make -k check
}

package() {
    cd "${srcdir}/build-${CHOST}"
	make DESTDIR="${pkgdir}" install
}
