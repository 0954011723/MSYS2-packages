# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=git
pkgver=2.0.1
pkgrel=1
pkgdesc="The fast distributed version control system"
arch=('i686' 'x86_64')
url="http://git-scm.com/"
license=('GPL2')
groups=('VCS')
depends=('curl' 'expat>=2.0' 'perl-Error' 'perl>=5.14.0' 'openssl' 'libpcre' 'vim')
makedepends=('libcurl-devel' 'python2' 'less' 'openssh' 'rsync')
optdepends=(#'tk: gitk and git gui'
            'perl-libwww: git svn'
            'perl-TermReadkey: git svn'
            'perl-MIME-tools: git send-email'
            'perl-Net-SMTP-SSL: git send-email TLS support'
            'perl-Authen-SASL: git send-email TLS support'
            'python2: various helper scripts'
            'subversion: git svn')
            #'cvsps: git cvsimport'
replaces=('git-core')
provides=('git-core')
source=(#"http://git-core.googlecode.com/files/git-$pkgver.tar.gz"
        #"http://git-core.googlecode.com/files/git-manpages-$pkgver.tar.gz"
        http://fossies.org/linux/misc/git-$pkgver.tar.gz
        http://fossies.org/linux/misc/git-manpages-$pkgver.tar.gz
        1.7.9-cygwin.patch
        git-1.9.0-manifest-msys2.patch
        git-1.8.4-msys2.patch)
md5sums=('981f5937840716cb563be1cc6292c8d7'
         'b36a03d806207ebd38913fcc4e8053a6'
         'c33c9dfa2944a0de9151a7af7a63a27b'
         '6091627ea0b56ba4a7e3b71ed40605ed'
         '9f9170a30dfb7643df4948cdae5bb864')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  
  patch -p2 -i ${srcdir}/1.7.9-cygwin.patch
  patch -p1 -i ${srcdir}/git-1.9.0-manifest-msys2.patch
  patch -p1 -i ${srcdir}/git-1.8.4-msys2.patch
  
  make configure
}

build() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --build=${CHOST} \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-libpcre \
    --with-editor=vim \
    --htmldir=/usr/share/doc/git/html \
    --mandir=/usr/share/man

  make INSTALLDIRS=vendor all
}

check() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  local jobs
  jobs=$(expr "$MAKEFLAGS" : '.*\(-j[0-9]*\).*')
  mkdir -p /tmp/git-test
  # We used to use this, but silly git regressions:
  #GIT_TEST_OPTS="--root=/dev/shm/" \
  # http://comments.gmane.org/gmane.comp.version-control.git/202020
  make \
    NO_SVN_TESTS=y \
    DEFAULT_TEST_TARGET=prove \
    GIT_PROVE_OPTS="$jobs -Q" \
    GIT_TEST_OPTS="--root=/tmp/git-test" \
    test
}

package() {
  export PYTHON_PATH='/usr/bin/python2'
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALLDIRS=vendor DESTDIR="$pkgdir" install

  # bash completion
  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  install -m644 ./contrib/completion/git-completion.bash "$pkgdir"/usr/share/bash-completion/completions/git
  # fancy git prompt
  mkdir -p "$pkgdir"/usr/share/git/
  install -m644 ./contrib/completion/git-prompt.sh "$pkgdir"/usr/share/git/git-prompt.sh

  # the rest of the contrib stuff
  cp -a ./contrib/* $pkgdir/usr/share/git/

  # scripts are for python 2.x
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find "$pkgdir" -name '*.py') \
    "$pkgdir"/usr/lib/git-core/git-p4 \
    "$pkgdir"/usr/share/git/gitview/gitview \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-bzr \
    "$pkgdir"/usr/share/git/remote-helpers/git-remote-hg
  sed -i 's|#![ ]*/usr/bin/python$|#!/usr/bin/python2|' \
    "$pkgdir"/usr/share/git/svn-fe/svnrdump_sim.py


  # how about some manpages?
  for mansect in man1 man5 man7; do
    for manpage in "$srcdir"/$mansect/*; do
      install -D -m644 $manpage "$pkgdir"/usr/share/man/$mansect/$(basename $manpage)
    done
  done

  # remove perllocal.pod, .packlist, and empty directories.
  find $pkgdir/usr/lib/perl5 -name *.pod -delete
  find $pkgdir/usr/lib/perl5 -name .packlist -delete
}
