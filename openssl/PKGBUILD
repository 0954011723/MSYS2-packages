# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('openssl' 'libopenssl' 'openssl-devel')
_ver=1.0.1g
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('i686' 'x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('perl' 'zlib')
makedepends=('gcc' 'tar' 'zlib-devel')
optdepends=('ca-certificates')
options=('!makeflags')
noextract=(${pkgname}-${_ver}.tar.gz)
#backup=('etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"{,.asc}
        'no-rpath.patch'
        'ca-dir.patch'
        'openssl-1.0.1f-manfix.patch'
        'openssl-1.0.1e-cygwin64.patch'
        'openssl-1.0.1e-msys2.patch'
        'openssl-1.0.0-beta5-enginesdir.patch')
md5sums=('de62b43dfcd858e66a74bee1c834e959'
         'SKIP'
         'dc78d3d06baffc16217519242ce92478'
         '5542c0310f58057fb1b8b199c7c04865'
         'ea2a61c8bd43788d81d98f1ac36c98ac'
         '4674951d9fbf4ca4564580a45ed2fe98'
         'd71b4d8a1d124ad930f30a0b2649eb71'
         '5d5598338fc5f95b824e46a5fb303db0')

prepare() {
	cd $startdir/
	[ -d $srcdir/$pkgname-$_ver ] || tar -xzvf ${pkgname}-${_ver}.tar.gz -C $srcdir
	cd $srcdir/$pkgname-$_ver

	# remove rpath: http://bugs.archlinux.org/task/14367
	patch -p0 -i $srcdir/no-rpath.patch
	# set ca dir to /etc/ssl by default
	patch -p0 -i $srcdir/ca-dir.patch

	patch -p1 -i $srcdir/openssl-1.0.1f-manfix.patch
	patch -p1 -i $srcdir/openssl-1.0.1e-cygwin64.patch
	patch -p1 -i $srcdir/openssl-1.0.1e-msys2.patch
	patch -p1 -i $srcdir/openssl-1.0.0-beta5-enginesdir.patch
}

build() {
	cd $srcdir/$pkgname-$_ver

	if [ "${CARCH}" == 'x86_64' ]; then
		openssltarget='Msys-x86_64'
	elif [ "${CARCH}" == 'i686' ]; then
		openssltarget='Msys'
	fi

	./Configure --prefix=/usr \
		--openssldir=/usr/ssl \
		--enginesdir=/usr/lib/openssl/engines \
		--libdir=lib \
		shared \
		zlib \
		no-idea \
		no-rc5 \
		no-asm \
		${openssltarget}
		
	make depend
	make
	make INSTALL_PREFIX=${srcdir}/dest MANDIR=/usr/share/man MANSUFFIX=ssl install
}

check() {
	cd $srcdir/$pkgname-$_ver

	make test
}

package_openssl() {
    depends=('libopenssl' 'perl' 'zlib')
	
	mkdir -p $pkgdir/usr/bin
	cp -f ${srcdir}/dest/usr/bin/*.exe $pkgdir/usr/bin/
	cp -f ${srcdir}/dest/usr/bin/c_rehash $pkgdir/usr/bin/
	cp -rf ${srcdir}/dest/usr/share $pkgdir/usr/
	cp -rf ${srcdir}/dest/usr/ssl $pkgdir/usr/
	install -D -m644 $srcdir/$pkgname-$_ver/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_libopenssl() {
	depends=('zlib')
	groups=('libraries')

	mkdir -p $pkgdir/usr/bin
	cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/

	mkdir -p $pkgdir/usr/lib/openssl
	cp -rf $srcdir/dest/usr/lib/engines ${pkgdir}/usr/lib/openssl/
	chmod -R 755 ${pkgdir}/usr/lib/openssl/engines
}

package_openssl-devel() {
	pkgdesc="Openssl headers and libraries"
	groups=('development')
	depends=("libopenssl=${pkgver}")

	mkdir -p $pkgdir/usr/lib
	cp -rf $srcdir/dest/usr/include $pkgdir/usr/
	cp -rf $srcdir/dest/usr/lib/pkgconfig $pkgdir/usr/lib/
	cp -f $srcdir/dest/usr/lib/*.a $pkgdir/usr/lib
}
