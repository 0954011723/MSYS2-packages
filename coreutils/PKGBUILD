# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=coreutils
pkgver=8.22
pkgrel=1
pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('gmp' 'libiconv' 'libintl')
makedepends=('gmp-devel' 'libiconv-devel' 'gettext-devel' 'help2man')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        coreutils-8.15-3.src.patch
    coreutils-8.15-fix-dummy-man-invocation.patch
    coreutils-8.15-fix-libstdbuf-link.patch
    coreutils-8.15-fix-man-deps.patch
    coreutils-8.15-fix-manpages.patch
    coreutils-8.15-manifest.patch
    coreutils-8.15-msys1-0x0d.patch
    coreutils-8.15-msys2-add-to-gnulib.patch
    coreutils-8.15-msys2-src.patch
    coreutils-8.15-msys2.patch)
md5sums=('8fb0ae2267aa6e728958adc38f8163a2'
         'SKIP'
         '2a2144d1ae5fb05dc0557677c906c22f'
         'c921582989dbfe87fdcc795c1795430e'
         'a8eef34fce0a141652c18aa8e5fb63aa'
         '52572688bd1039204aa4a06f4aa88547'
         'cef81924146c101a1fbcdb7d42c09c1c'
         'ac866880965af865040e543e768476d5'
         '9113031fae1116be3359fceadb15c6b7'
         'f5c8937709481a814de66728dd596883'
         '675153737e2a819f69a6f57a7ad9c839'
         '3eb55deabe4e3fe60caf3143912ce059')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p2 -i $srcdir/coreutils-8.15-3.src.patch
  patch -p2 -i $srcdir/coreutils-8.15-fix-dummy-man-invocation.patch
  patch -p2 -i $srcdir/coreutils-8.15-fix-libstdbuf-link.patch
  patch -p2 -i $srcdir/coreutils-8.15-fix-man-deps.patch
  patch -p2 -i $srcdir/coreutils-8.15-fix-manpages.patch
  patch -p2 -i $srcdir/coreutils-8.15-manifest.patch
  patch -p2 -i $srcdir/coreutils-8.15-msys1-0x0d.patch
  patch -p2 -i $srcdir/coreutils-8.15-msys2-add-to-gnulib.patch
  patch -p1 -i $srcdir/coreutils-8.15-msys2-src.patch
  patch -p1 -i $srcdir/coreutils-8.15-msys2.patch
}


build() {
  [[ -d $srcdir/build-${CHOST} ]] && rm -rf $srcdir/build-${CHOST}
  mkdir -p $srcdir/build-${CHOST} && cd $srcdir/build-${CHOST}
  
  #cd ${srcdir}/${pkgname}-${pkgver}
  ../${pkgname}-${pkgver}/configure --build=${CHOST} \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --program-transform-name=s/kill/gkill/ \
    --without-libintl-prefix \
    --without-libiconv-prefix \
    --enable-install-program=arch,hostname \
    --enable-no-install-program=uptime

  make -j1
}

package() {
  cd $srcdir/build-${CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/etc
  install -m644 ${srcdir}/${pkgname}-${pkgver}/src/dircolors.hin ${pkgdir}/usr/etc/DIR_COLORS
}
