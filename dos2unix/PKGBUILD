# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=dos2unix
pkgver=6.0.3
pkgrel=1
pkgdesc='Text file format converter'
arch=('i686' 'x86_64')
url='http://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('libintl')
groups=('base-devel')
makedepends=('perl')
conflicts=('hd2u')
source=("http://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    0001-msysize.msys2.patch)
sha256sums=('f5268c5b28a1983537210c72b3fd43f4aad17a8dacb6727b615b56dca2ef9a04'
            '8b7d35bda25377b23bc49e7a8796cd9a239c46973eb8c9351da03390ae6a591a')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i ${srcdir}/0001-msysize.msys2.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm755 $pkgdir/usr/bin/dos2unix.exe $pkgdir/usr/bin/d2u.exe
  install -Dm755 $pkgdir/usr/bin/unix2dos.exe $pkgdir/usr/bin/u2d.exe
  install -D -m644 COPYING.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}