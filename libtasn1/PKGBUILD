# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libtasn1' 'libtasn1-devel')
pkgver=3.3
pkgrel=1
pkgdesc="A library for Abstract Syntax Notation One (ASN.1) and Distinguish Encoding Rules (DER) manipulation."
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('texinfo')
install=libtasn1.install
source=(http://ftp.gnu.org/gnu/libtasn1/${pkgname}-${pkgver}.tar.gz{,.sig}
        libtasn1-3.3-msys2.patch)
md5sums=('2bdd687f487c50a0382b99553866a6e0'
         'SKIP'
         'ad275113015f81a542de14ff0bf10721')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i $srcdir/libtasn1-3.3-msys2.patch
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --build=${CHOST} --prefix=/usr
  make
  make DESTDIR=$srcdir/dest install
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package_libtasn1() {
  groups=('libraries') 
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/share $pkgdir/usr/
}

package_libtasn1-devel() {
  pkgdesc="Libtasn1 headers and libraries"
  groups=('development')
  
  mkdir -p $pkgdir/usr/bin
  cp -f $srcdir/dest/usr/bin/*.exe $pkgdir/usr/bin/
  cp -rf $srcdir/dest/usr/include $pkgdir/usr/
  cp -rf $srcdir/dest/usr/lib $pkgdir/usr/
}