# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('curl' 'libcurl' 'libcurl-devel')
pkgver=7.34.0
pkgrel=2
pkgdesc="Multi-protocol file transfer utility"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'libidn' 'libmetalink' 'libssh2' 'openssl' 'zlib' 'heimdal-libs')
makedepends=('libmetalink-devel' 'libidn-devel' 'libssh2-devel' 'openssl-devel' 'zlib-devel' 'heimdal-devel')
#provides=('libcurl.so')
options=('!libtool' 'strip' '!debug')
source=("http://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        curl-7.32.0-msys2.patch
		7.33.0-libdeps.patch
		0001-connect-Try-all-addresses-in-first-connection-attemp.patch
		0001-FILE-don-t-wait-due-to-CURLOPT_MAX_RECV_SPEED_LARGE.patch)
md5sums=('d5b7edccbd1793e3549842e01331da20'
         'SKIP'
         '255b3bf8fe8704adfb95222865589d16'
         '009c98eea0b41ad0e534f5fac2bddd96'
         'ee755b0e886207fc1e8154e426c46ca7'
         'ba766acdb7568aac0b23d479ebecd591')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/curl-7.32.0-msys2.patch
  patch -p2 -i ${srcdir}/7.33.0-libdeps.patch
  patch -p1 -i ${srcdir}/0001-connect-Try-all-addresses-in-first-connection-attemp.patch
  patch -p1 -i ${srcdir}/0001-FILE-don-t-wait-due-to-CURLOPT_MAX_RECV_SPEED_LARGE.patch
  
  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --build=${CHOST} \
      --prefix=/usr \
      --enable-debug \
      --enable-optimize \
	  --enable-ipv6 \
      --disable-hidden-symbols \
      --disable-ares \
      --with-gssapi \
      --with-libidn \
      --with-libmetalink \
      --without-librtmp \
      --with-libssh2 \
      --without-spnego \
      --with-ssl \
      --with-zlib \
      --with-ca-bundle=/usr/ssl/certs/ca-bundle.crt \
	  ac_cv_func_setmode=yes

  make
  make DESTDIR="${srcdir}/dest" install
}

package_curl() {
  depends=('ca-certificates' 'libcurl' 'libmetalink' 'openssl' 'zlib')
  groups=('base')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/

  # license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_libcurl() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('ca-certificates' 'heimdal-libs' 'libidn' 'libmetalink' 'libssh2' 'openssl' 'zlib')
  groups=('libraries')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcurl-devel() {
  pkgdesc="Libcurl headers and libraries"
  depends=("libcurl=${pkgver}")
  options=('!staticlibs')
  groups=('development')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
