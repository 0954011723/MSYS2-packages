# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('curl' 'libcurl' 'libcurl-devel')
pkgver=7.38.0
pkgrel=1
pkgdesc="Multi-protocol file transfer utility"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'libcares' 'libcrypt' 'libidn' 'libmetalink' 'libssh2' 'openssl' 'zlib' 'heimdal-libs')
makedepends=('libmetalink-devel' 'libcares-devel' 'libcrypt-devel' 'libidn-devel' 'libssh2-devel' 'openssl-devel' 'zlib-devel' 'heimdal-devel')
#provides=('libcurl.so')
options=('!libtool' 'strip' '!debug')
source=("http://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        curl-7.32.0-msys2.patch
        7.33.0-libdeps.patch)
md5sums=('b6e3ea55bb718f2270489581efa50a8a'
         '46a4fd106498d6ccad8f33f9bd15aa70'
         '859cee7c1510c581dc924cc7417dd179'
         '009c98eea0b41ad0e534f5fac2bddd96')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/curl-7.32.0-msys2.patch
  patch -p2 -i ${srcdir}/7.33.0-libdeps.patch
  
  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-debug \
    --enable-optimize \
    --enable-ipv6 \
    --disable-hidden-symbols \
    --disable-ares \
    --with-gssapi \
    --with-libidn \
    --with-libmetalink \
    --without-librtmp \
    --with-libssh2 \
    --without-spnego \
    --with-ssl \
    --with-zlib \
    --with-ca-bundle=/usr/ssl/certs/ca-bundle.crt \
    ac_cv_func_setmode=yes

  make
  make DESTDIR="${srcdir}/dest" install
}

package_curl() {
  depends=('ca-certificates' 'libcurl' 'libcrypt' 'libmetalink' 'openssl' 'zlib')
  groups=('base')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/

  # license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_libcurl() {
  pkgdesc="Multi-protocol file transfer library (runtime)"
  depends=('ca-certificates' 'heimdal-libs' 'libcares' 'libcrypt' 'libidn' 'libmetalink' 'libssh2' 'openssl' 'zlib')
  groups=('libraries')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.dll ${pkgdir}/usr/bin/
}

package_libcurl-devel() {
  pkgdesc="Libcurl headers and libraries"
  depends=("libcurl=${pkgver}" 'libcares-devel' 'libcrypt-devel' 'libmetalink-devel' 'libidn-devel' 'libssh2-devel' 'openssl-devel' 'zlib-devel' 'heimdal-devel')
  options=('!staticlibs')
  groups=('development')
  
  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*-config ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
