# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=less
pkgver=550
pkgrel=1
pkgdesc="A terminal based program for viewing text files"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
groups=('base')
depends=('ncurses' 'libpcre')
makedepends=('ncurses-devel' 'pcre-devel')
source=(http://www.greenwoodsoftware.com/${pkgname}/${pkgname}-${pkgver}{.tar.gz,.sig})
sha256sums=('6a53639f08d7ed05b6e104b82c32193f79ac01a3eddb20e114e1c261948bd57b'
            'SKIP')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure \
      --build=${CHOST} \
      --prefix=/usr \
      --sysconfdir=/etc \
      --with-regex=pcre
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
