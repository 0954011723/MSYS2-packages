# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=scons
pkgver=2.3.0
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('any')
url="http://scons.org"
license=('MIT')
depends=('python2')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
		cygwin-use_usr_bin.patch
		scons-2.3.0-cyglink-msys2.patch)
md5sums=('083ce5624d6adcbdaf2526623f456ca9'
         'e1f81fdd3b0748674d982b2ecbc379c5'
         'a071cac45a7bb82c1c75366ad3b7ed88')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  
  patch -p2 -i ${srcdir}/cygwin-use_usr_bin.patch
  patch -p1 -i ${srcdir}/scons-2.3.0-cyglink-msys2.patch
}


package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --standard-lib \
                           --prefix=/usr \
                           --install-data=/usr/share \
                           --optimize=1 \
                           --root="$pkgdir"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' \
    "$pkgdir"/usr/bin/*

  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
