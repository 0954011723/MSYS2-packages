# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=scons
pkgver=2.3.2
pkgrel=1
pkgdesc="Extensible Python-based build utility"
arch=('any')
groups=('base-devel')
url="http://scons.org"
license=('MIT')
depends=('python2')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        cygwin-use_usr_bin.patch
        scons-2.3.1-cyglink-msys2.patch)
md5sums=('bbd428da35ec176575a2b0be1f8d7162'
         'e1f81fdd3b0748674d982b2ecbc379c5'
         '468f1c1d1cdaf109fe3e1a45594b6413')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p2 -i ${srcdir}/cygwin-use_usr_bin.patch
  patch -p1 -i ${srcdir}/scons-2.3.1-cyglink-msys2.patch
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  /bin/python2 setup.py install \
      --standard-lib \
      --prefix=/usr \
      --install-data=/usr/share \
      --optimize=1 \
      --root="$pkgdir"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/python2:' \
    "$pkgdir"/usr/bin/*

  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
