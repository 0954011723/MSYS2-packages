# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=help2man
pkgver=1.43.3
pkgrel=1
pkgdesc="Conversion tool to create man files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/help2man/"
license=('GPL')
depends=('perl-locale-gettext' 'libintl')
install=help2man.install
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
		1.40.4-cygwin-nls.patch)
md5sums=('a84868db7c139238df8add5d86a0b54f'
         'dcd0c0843e177d264f4199e4ea84d142')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p2 -i ${srcdir}/1.40.4-cygwin-nls.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
              --prefix=/usr \
              --mandir=/usr/share/man \
	          --infodir=/usr/share/info \
	          --libdir=/usr/lib
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
