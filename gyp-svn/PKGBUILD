# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=gyp-svn
pkgver=r1847
pkgrel=1
pkgdesc="GYP can Generate Your Projects."
url="http://code.google.com/p/gyp/"
arch=('i686' 'x86_64')
provides=("gyp")
conflicts=("gyp")
makedepends=('subversion')
depends=('python2' 'python2-setuptools')
license=('custom')
source=("gyp::svn+http://gyp.googlecode.com/svn/trunk/"
		'gyp-msysize.patch')
md5sums=('SKIP'
         'a3e563b25bb844bac194a1926c82b0ea')

pkgver() {
  cd $srcdir/gyp
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd ${srcdir}/gyp
  patch -p1 -i ${srcdir}/gyp-msysize.patch
}

package() {
  cd ${srcdir}/gyp
  python2 setup.py install --root=$pkgdir
}
