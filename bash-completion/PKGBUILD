# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=bash-completion
pkgver=2.1
pkgrel=2
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
groups=('base')
depends=('bash')
options=('!emptydirs' '!makeflags')
source=(http://bash-completion.alioth.debian.org/files/${pkgname}-${pkgver}.tar.bz2
		bash-4.3.patch
		bash-completion-lz4.patch
		words_bad_array_subscript.patch)
md5sums=('4e2a9f11a4042a38ee79ddcd048e8b9e'
         'f964553680d88a89dbe8a7275e94bd3b'
         '54a801a7a2b7aaae6ad7b3b6419003fe'
         'f524a2d082fe5f799ab262c91a0cd507')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/bash-4.3.patch
  #patch -p0 -i ${srcdir}/bash-completion-lz4.patch
  patch -p1 -i ${srcdir}/words_bad_array_subscript.patch
  
  autoreconf --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  mv -f ${pkgdir}/etc ${pkgdir}/usr/
  mkdir -p ${pkgdir}/usr/lib
  mv -f ${pkgdir}/usr/share/pkgconfig ${pkgdir}/usr/lib/
# bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
#  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

# remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"

}
