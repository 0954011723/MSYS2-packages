# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pacman
_base_ver=4.1.2
pkgver=4.1.2.5700.652c183
pkgrel=3
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.045'
         'msys2-runtime'
         'libarchive'
         'curl>=7.19.4'
         'libgpgme'
         'pacman-mirrors'
         'which'
         'bzip2'
         'xz')
checkdepends=('python2')
makedepends=('autoconf' 'automake-wrapper' 'libtool' 'git' 'gettext-devel' 'libarchive-devel' 'libcurl-devel' 'libgpgme-devel')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
#options=('!strip' 'debug')
backup=(#"usr/etc/pacman.conf"
        "usr/etc/makepkg.conf"
        "usr/etc/makepkg_mingw32.conf"
        "usr/etc/makepkg_mingw64.conf")
source=("$pkgname"::'git://github.com/Alexpux/MSYS2-pacman.git'
        "0001-change-int-size-to-off_t-size-in-add_transaction_siz.patch"
        "pacman.conf.i686"
        "pacman.conf.x86_64"
        "makepkg.conf"
        "makepkg_mingw32.conf"
        "makepkg_mingw64.conf"
        "makepkg-mingw")
md5sums=('SKIP'
         '7e947628e02f45e8d497850446827525'
         'b079f3989407f048439649cf7f5b183a'
         'b079f3989407f048439649cf7f5b183a'
         'c48e0d174c906db8c0f28482121f2182'
         'a269cef4f776335a0fadced6a474a000'
         '059f4769037927fc6b4f2f02b8e6af5a'
         '2ed9a2406554fc69b4f3f4474a7c03d6')

pkgver() {
  cd "$srcdir/$pkgname"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/$pkgname
  git am ${srcdir}/0001-change-int-size-to-off_t-size-in-add_transaction_siz.patch
}

build() {
  cd $srcdir/$pkgname
  ./autogen.sh

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
    --with-pkg-ext=.pkg.tar.xz
  make
  make -C contrib
}

check() {
  make -C "$pkgname" check
}

package() {
  cd $srcdir/$pkgname
  make -j1 DESTDIR=$pkgdir install
  make -j1 DESTDIR=$pkgdir -C contrib install

  # Workaround for MSYS - place all files in /usr
  mv -f $pkgdir/etc $pkgdir/usr/
  mv -f $pkgdir/var $pkgdir/usr/

  # install Arch specific stuff
  install -dm755 $pkgdir/usr/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/usr/etc/pacman.conf

  case "$CARCH" in
  i686)    
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 $srcdir/makepkg.conf $pkgdir/usr/etc/
  install -m644 $srcdir/makepkg_mingw32.conf $pkgdir/usr/etc/
  install -m644 $srcdir/makepkg_mingw64.conf $pkgdir/usr/etc/
  install -m755 $srcdir/makepkg-mingw $pkgdir/usr/bin/

  # set things correctly in the default conf file
  sed -i $pkgdir/usr/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/usr/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/usr/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}
