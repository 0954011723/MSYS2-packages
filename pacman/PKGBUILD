# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pacman
_base_ver=4.1.2
pkgver=4.1.2.5561.b716664
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64')
url=""
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.045' 'msys2-runtime' 'libarchive>=3.1.2' 'curl>=7.19.4'
         'libgpgme')
checkdepends=('python2')
makedepends=('git')
optdepends=()
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=("usr/etc/pacman.conf" "usr/etc/makepkg.conf" "usr/etc/makepkg_mingw32.conf" "usr/etc/makepkg_mingw64.conf")
source=("$pkgname"::'git://github.com/Alexpux/MSYS2-pacman.git'
		"pacman.conf.i686"
		"pacman.conf.x86_64"
		"makepkg.conf"
		"makepkg_mingw32.conf"
		"makepkg_mingw64.conf"
		"makepkg-mingw")
md5sums=('SKIP'
         'c5e8e1fac90fa490347d8cda49aea282'
         'c5e8e1fac90fa490347d8cda49aea282'
         'fc4f7b4393304307f41c219beb3b4623'
         '541a6a0ad0972b77d3a5d1aef1d0bbb1'
         '3df7a12326723e3f7ae0ba08d7a7858b'
         '68026294d8665b4561b23074b55594c2')

pkgver() {
  cd "$srcdir/$pkgname"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $srcdir/$pkgname
  ./autogen.sh

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc \
    --with-scriptlet-shell=/usr/bin/bash \
	--with-pkg-ext=.pkg.tar.xz
  make
  make -C contrib
}

check() {
  make -C "$pkgname" check
}

package() {
  cd $srcdir/$pkgname
  make -j1 DESTDIR=$pkgdir install
  make -j1 DESTDIR=$pkgdir -C contrib install
  
  # Workaround for MSYS - place all files in /usr
  mv -f $pkgdir/etc $pkgdir/usr/
  mv -f $pkgdir/var $pkgdir/usr/

  # install Arch specific stuff
  install -dm755 $pkgdir/usr/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/usr/etc/pacman.conf
  
  case "$CARCH" in
    i686)    
      mycarch="i686"
      mychost="i686-pc-msys"
	  mymingw="i686-w64-mingw32"
      myflags="-march=i686"
	  myprefix="/mingw32"
      ;;
    x86_64)
      mycarch="x86_64"
      mychost="x86_64-pc-msys"
	  mymingw="x86_64-w64-mingw32"
      myflags="-march=x86-64"
	  myprefix="/mingw64"
      ;;
  esac
  
  install -m644 $srcdir/makepkg.conf $pkgdir/usr/etc/
  install -m644 $srcdir/makepkg_mingw32.conf $pkgdir/usr/etc/
  install -m644 $srcdir/makepkg_mingw64.conf $pkgdir/usr/etc/
  install -m755 $srcdir/makepkg-mingw $pkgdir/usr/bin/
  
  # set things correctly in the default conf file
  sed -i $pkgdir/usr/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
	-e "s|@MINGW_CHOST[@]|$mymingw|g" \
	-e "s|@MINGW_PREFIX[@]|$myprefix|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
    
  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/usr/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/usr/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}
