# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=pacman
_base_ver=4.1.2
pkgver=4.1.2.5831.8f603c2
pkgrel=2
pkgdesc="A library-based package manager with dependency support (MSYS2 port)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('bash>=4.2.045'
         'dash'
         'busybox'
         'gettext'
         'msys2-runtime'
         'libarchive'
         'curl>=7.19.4'
         'libgpgme'
         'pacman-mirrors'
         'msys2-keyring'
         'which'
         'bzip2'
         'xz')
optdepends=('diffutils' 'vim')
checkdepends=('python2')
makedepends=('asciidoc'
             'autoconf'
             'automake-wrapper'
             'doxygen'
             'libtool'
             'git'
             'gettext-devel'
             'libarchive-devel'
             'libcurl-devel'
             'libgpgme-devel')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
# options=('!strip' 'debug')
backup=("etc/pacman.conf"
        "etc/makepkg.conf"
        "etc/makepkg_mingw32.conf"
        "etc/makepkg_mingw64.conf")
source=("$pkgname"::'git://github.com/Alexpux/MSYS2-pacman.git'
        "pacman.conf.i686"
        "pacman.conf.x86_64"
        "makepkg.conf"
        "makepkg_mingw32.conf"
        "makepkg_mingw64.conf"
        "makepkg-mingw"
        "0001-more-debugging-info.patch"
        "0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch"
        "0003-use-busybox-for-msys2-post-installs.patch")
md5sums=('SKIP'
         '8fa93febeebaeb62dfa7b63d2846ce52'
         '8fa93febeebaeb62dfa7b63d2846ce52'
         'c259ae2153751ce7343c2937644e4950'
         'eac2f0002729f0db4e841e2594d14123'
         'c8859429e8bc1abc3e170fac912df58d'
         '6d09ad2af496334e3f9f546c1e586676'
         '9cbad99274b1013e91ddc1e311a79cc5'
         'a2e1a5c77c2ede63d7b843c9b7b39c6f'
         'd05c820b244e571aa5a515a516299871')

pkgver() {
  cd "$srcdir/$pkgname"
  #printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  printf "%s.%s.%s" "${_base_ver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $srcdir/$pkgname
  git am "$srcdir"/0001-more-debugging-info.patch
  git am "$srcdir"/0002-Add-util-msys2.-c-h-and-rebase-db-msys2.-c.patch
  git am "$srcdir"/0003-use-busybox-for-msys2-post-installs.patch
}

build() {
  cd $srcdir/$pkgname
  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-doc \
    --enable-doxygen \
    --with-scriptlet-shell=/usr/bin/dash \
    --with-pkg-ext=.pkg.tar.xz
  make -j1
  make -C contrib
}

check() {
  make -C "$pkgname" check
}

package() {
  cd $srcdir/$pkgname
  make -j1 DESTDIR=$pkgdir install
  make -j1 DESTDIR=$pkgdir -C contrib install

  # install Arch specific stuff
  install -dm755 $pkgdir/etc
  install -m644 $srcdir/pacman.conf.$CARCH $pkgdir/etc/pacman.conf

  case "$CARCH" in
  i686)    
    mycarch="i686"
    mychost="i686-pc-msys"
    myflags="-march=i686"
  ;;
  x86_64)
    mycarch="x86_64"
    mychost="x86_64-pc-msys"
    myflags="-march=x86-64"
  ;;
  esac

  install -m644 $srcdir/makepkg.conf $pkgdir/etc/
  install -m644 $srcdir/makepkg_mingw32.conf $pkgdir/etc/
  install -m644 $srcdir/makepkg_mingw64.conf $pkgdir/etc/
  install -m755 $srcdir/makepkg-mingw $pkgdir/usr/bin/

  # set things correctly in the default conf file
  sed -i $pkgdir/etc/makepkg.conf \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$mychost|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"

  # put bash_completion in the right location
  install -dm755 ${pkgdir}/usr/share/bash-completion/completions
  mv ${pkgdir}/etc/bash_completion.d/pacman \
    ${pkgdir}/usr/share/bash-completion/completions
  rmdir ${pkgdir}/etc/bash_completion.d

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done
}
