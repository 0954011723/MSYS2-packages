# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Probe-Perl
pkgname=perl-${_realname}
pkgver=0.03
pkgrel=1
pkgdesc="Information about the currently running perl"
arch=('any')
url="http://github.com/kenahoo/Probe-Perl/"
groups=('perl-modules')
depends=('perl')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/K/KW/KWILLIAMS/${_realname}-${pkgver}.tar.gz"
        'Probe-Perl-0.01.patch')
sha1sums=('7c3cdb0f5d7887d8af6fda4002eb32026518ce87'
          '0000af11892457eec32c9beb09e51eedff25fd6d')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/Probe-Perl-0.01.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
