# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rebase
pkgver=4.4.0.1
pkgrel=2
pkgdesc="The Cygwin rebase distribution contains four utilities, rebase, rebaseall, peflags, and peflagsall."
groups=('base')
arch=('i686' 'x86_64')
license=('custom')
url="http://www.cygwin.com/"
depends=('msys2-runtime' 'dash')
makedepends=('coreutils' 'grep' 'gzip' 'sed')
source=(rebase-4.4.0.1.tar.bz2
        'rebase-4.4.0-msys2.patch'
		'autorebase.bat')
sha1sums=('2ae29764a6d7f969b4da3a56d4a0ad97184bdaf7'
          '0df8af56cda5f1512798f8d4c216b684bcad5554'
          'e25b323bda0e71fd7cda44c42be3945052d809c9')

prepare() {
  cd "$srcdir/$pkgname"
  
  patch -p1 -i $srcdir/rebase-4.4.0-msys2.patch
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    CXXFLAGS="-static -static-libgcc -static-libstdc++" \
	CFLAGS="-static -static-libgcc"
  make
}

package() {
  cd "$srcdir/$pkgname"
  
  make DESTDIR=${pkgdir} install
  cp -f ${srcdir}/autorebase.bat ${pkgdir}/usr
}
