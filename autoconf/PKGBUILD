# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=autoconf
pkgver=2.69
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('awk' 'm4' 'diffutils' 'bash')
# for test-suite
makedepends=('gcc-fortran')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        autoconf-2.69-fix-texinfo.patch
        autoconf-2.69-msys2.patch)
md5sums=('50f97f4159805e374639a73e2636f22e'
         'SKIP'
         'e5065f5c776dbb482a337d4ab105a083'
         '6f39b8f28a7d94976a3cdd5a055d1870')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/autoconf-2.69-fix-texinfo.patch
  patch -p1 -i ${srcdir}/autoconf-2.69-msys2.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  install -Dm644 COPYING.EXCEPTION \
    $pkgdir/usr/share/licenses/autoconf/COPYING.EXCEPTION

  # conflict with bintuils
  rm -f ${pkgdir}/usr/share/info/standards.info
}
