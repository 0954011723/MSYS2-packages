# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=(icu icu-devel)
pkgver=51.2
pkgrel=1
pkgdesc="International Components for Unicode library"
arch=('i686' 'x86_64')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('gcc-libs' 'sh')
source=(http://download.icu-project.org/files/${pkgname}4c/${pkgver}/${pkgname}4c-${pkgver//./_}-src.tgz
        51.1-cygwin-no-w32api.patch
		51.1-cygwin-tests.patch
		51.1-cygwin-tools.patch
		51.1-cygwin-x64.patch
		icu-51.2-msys2.patch)
sha256sums=('deb027a05f1b3bec03298b96fb93b28c84e9683c22e6f94effa67fdc7bd704cc'
            '9a0045f8670308f6f6fb218a4cac02411929893c86065fd675d8d939a088eb19'
            'd62f62df644a944aead5d66c94c686693dc1aab710506fa539d99e7f6d8abc9c'
            '300d6bd9a3224ca2707efe9542de3246ba11652815b17d49e13dece023df9641'
            'dcc35bea3906ed659291ab52c7d766634f4c368aa2ff128b7ebf96638e82f052'
            'baf87e577a8226d31f5169495cb0ccb8a647b3dce73cccc9f6d80bceb2ad8a03')

prepare() {
	cd ${srcdir}/icu/source
	patch -p2 -i ${srcdir}/51.1-cygwin-no-w32api.patch
	patch -p2 -i ${srcdir}/51.1-cygwin-tests.patch
	patch -p2 -i ${srcdir}/51.1-cygwin-tools.patch
	patch -p2 -i ${srcdir}/51.1-cygwin-x64.patch
	patch -p2 -i ${srcdir}/icu-51.2-msys2.patch

	libtoolize --force --copy
	autoreconf -fi
}
  
build() {
	cd ${srcdir}/icu/source
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--sbindir=/usr/bin
	make

	make -j1 DESTDIR=${srcdir}/dest install
	pushd ${srcdir}/dest/usr/lib
		# fix installation directory
		mv msys-icu*${pkgver%%.*}.dll ${srcdir}/dest/usr/bin/
		# remove unversioned DLL symlinks
		rm -f *.dll
		# create unversioned implib symlinks
		for l in *.dll.a; do
			#dosym ${l} /usr/lib/${l//${pkgver%%.*}}
			cp ${l} ${l//${pkgver%%.*}}
		done
	popd

	sed -e '/^ICUUC_FILE=/ s/libdir/bindir/' \
		-e 's/l${LIBICU}/l${ICUPREFIX}/g' \
		-e '/l${ICUPREFIX}/ s/${ICULIBSUFFIX_VERSION}//g' \
		-i ${srcdir}/dest/usr/bin/icu-config \
		|| error "sed icu-config failed"

	# Install license
	install -Dm644 ${srcdir}/icu/license.html ${srcdir}/dest/usr/share/licenses/icu/license.html
}

check() {
	cd icu/source
	make -k check # passes all
}

package_icu() {
	depends=('gcc-libs')
	groups=('libraries')
	mkdir -p $pkgdir/usr/bin
	cp -f $srcdir/dest/usr/bin/*.dll $pkgdir/usr/bin/
}

package_icu-devel() {
	pkgdesc="ICU headers and libraries"
	groups=('development')
	depends=("icu=${pkgver}")
	
	cp -rf $srcdir/dest/usr $pkgdir/
	rm -f $pkgdir/usr/bin/*.dll

}
