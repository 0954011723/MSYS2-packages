# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=flex
pkgver=2.5.37
pkgrel=1
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('m4' 'sh' 'libiconv' 'libintl')
makedepends=('libiconv-devel' 'gettext-devel')
options=('staticlibs')
install=flex.install
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2
        lex.sh
        2.5.37-pr3552806.patch
        2.5.37-tests-bison26.patch
        2.5.37-vpath.patch
        flex-2.5.37-msys2.patch)
md5sums=('c75940e1fc25108f2a7b3ef42abdae06'
         'f725259ec23a9e87ee29e2ef82eda9a5'
         '318906338d8f8d5b774c3a1ecd47ebe5'
         '18fbd7e7aaa889d1d026a79dcc76fa09'
         '68f74b4a02893b4382e4fd42e1f0176a'
         'b5a5d864e1315084f27f8cb038daf80a')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  
  patch -p1 -i $srcdir/2.5.37-pr3552806.patch
  patch -p1 -i $srcdir/2.5.37-tests-bison26.patch
  patch -p2 -i $srcdir/2.5.37-vpath.patch
  patch -p1 -i $srcdir/flex-2.5.37-msys2.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --build=${CHOST} --prefix=/usr \
    --without-libiconv-prefix --without-libintl-prefix
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  
  # these tests used features removed in bison-2.6
  sed -i -e '/test-bison-yylloc/d' -e '/test-bison-yylval/d' tests/Makefile.in

  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
  install -Dm755 $srcdir/lex.sh $pkgdir/usr/bin/lex
  #cp -f $pkgdir/usr/bin/flex.exe $pkgdir/usr/bin/flex++

  install -Dm644 COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/license.txt
}

