# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

pkgname=make
pkgver=4.0
pkgrel=5
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('msys2-runtime' 'sh' 'guile')
makedepends=('gettext' 'gettext-devel' 'git' 'wget' 'msys2-runtime-devel')
install=$pkgname.install
#options=('debug' '!strip')
source=('http://ftp.gnu.org/gnu/make/make-4.0.tar.bz2'
        'make-autoconf.patch'
        '0001-job.c-set_child_handler_action_flags-SV-41341.patch'
        'gnumake-fix-cast.patch')
md5sums=('571d470a7647b455e3af3f92d79f1c18'
         'a81be0be0705d56124534078bbf32700'
         '425882277ba2874a134897ea39bb88d5'
         '77ab9e05b2a056968d1385efc0efa824')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/make-autoconf.patch
  patch -p1 -i ${srcdir}/0001-job.c-set_child_handler_action_flags-SV-41341.patch
  patch -p1 -i ${srcdir}/gnumake-fix-cast.patch
  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ${PWD}/configure --build=${CHOST} --prefix=/usr \
    ac_cv_dos_paths=yes --without-libintl-prefix --without-libiconv-prefix
  make -j1 all
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
