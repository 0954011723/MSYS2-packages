# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Net-DNS
pkgname=perl-${_realname}
pkgver=0.74
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver."
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
depends=('perl-Digest-HMAC' 'perl-Net-IP' 'perl')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz"
        'Net-DNS-0.73-msys2.patch')
sha1sums=('1b183448d22ea49bca0c4b85c365af527101a2ae'
          '76abe332c2efad0c99a28c654e792eafa4ae9336')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/Net-DNS-0.73-msys2.patch
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_realname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
