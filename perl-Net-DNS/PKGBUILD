# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Net-DNS
pkgname=perl-${_realname}
pkgver=0.72
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver."
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
depends=('perl-Digest-HMAC' 'perl-Net-IP' 'perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NL/NLNETLABS/${_realname}-$pkgver.tar.gz"
        'Net-DNS-0.72.patch')
md5sums=('393e48ec6f28abe5ed30204276e02775'
         '72cdf9f51ae753302a59b7fd14d442de')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/Net-DNS-0.72.patch
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_realname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
