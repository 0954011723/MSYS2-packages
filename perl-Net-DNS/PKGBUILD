# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=Net-DNS
pkgname=perl-${_realname}
pkgver=0.75
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver."
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
depends=('perl-Digest-HMAC' 'perl-Net-IP' 'perl')
checkdepends=('perl-Test-Pod')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz"
        'Net-DNS-0.75-msys2.patch')
sha1sums=('e57d08cfe61962d9de3cd78ec705a954e05f36bd'
          'f14ef827f0be0ade6b9b7e10e601c16c24bd6eee')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/Net-DNS-0.75-msys2.patch
}

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_realname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim: ts=2 sw=2 et ft=sh
