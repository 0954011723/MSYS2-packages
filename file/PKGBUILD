# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=file
pkgver=5.18
pkgrel=1
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base' 'base-devel')
url="http://www.darwinsys.com/file/"
depends=('zlib')
makedepends=('python2' 'python3' 'zlib-devel')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz
		file-5.17-msys2.patch
		file-5.15-fix-regexp.patch)
md5sums=('d420d8f2990cd344673acfbf8d76ff5a'
         '1538c425504bce1cd01126a385b70eb1'
         'f29d52a9c614967b5f3c7d67f0983103')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ${srcdir}/file-5.17-msys2.patch
	patch -p1 -i ${srcdir}/file-5.15-fix-regexp.patch

	autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--build=${CHOST} \
		--host=${CHOST} \
		--target=${CHOST} \
		--enable-fsect-man5
	make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR=${pkgdir} install

	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

	PYTHON_SITELIB=$(python2 -c 'from distutils.sysconfig import * ; print get_python_lib(0,0);')
	PYTHON3_SITELIB=$(python3 -c 'from distutils.sysconfig import * ; print(get_python_lib(0,0));')

	mkdir -p ${pkgdir}/$PYTHON_SITELIB
	mkdir -p ${pkgdir}/$PYTHON3_SITELIB
	cp -f ${srcdir}/${pkgname}-${pkgver}/python/magic.py ${pkgdir}/$PYTHON_SITELIB
	cp -f ${srcdir}/${pkgname}-${pkgver}/python/magic.py ${pkgdir}/$PYTHON3_SITELIB

	cat ${srcdir}/${pkgname}-${pkgver}/magic/Magdir/* > ${pkgdir}/usr/share/misc/magic
	cp -rf ${pkgdir}/usr/share/misc/magic ${pkgdir}/usr/share/magic
	mkdir -p ${pkgdir}/usr/share/file
	cp -rf ${pkgdir}/usr/share/magic ${pkgdir}/usr/share/file/magic
}
