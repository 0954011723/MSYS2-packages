# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

pkgname=elinks-git
_ver_base=0.13
pkgver=0.13.3945.7453b03
pkgrel=1
epoch=
pkgdesc="Full-Featured Text WWW Browser (net-utils)"
arch=("i686" "x86_64")
url="http://elinks.cz/"
license=('GPL')
groups=('net-utils')
depends=('zlib'
         'libbz2'
		 'libopenssl'
		 'libiconv'
		 'libexpat'
		 'liblzma'
		 'gettext'
		 'libidn'
		 'doxygen'
		 'perl'
		 'python2'
		 'xmlto'
		 'libtre')
makedepends=('git'
             'zlib-devel'
			 'libbz2-devel'
			 'openssl-devel'
			 'libiconv-devel'
			 'libexpat-devel'
			 'liblzma-devel'
			 'gettext-devel'
			 'libidn-devel'
			 'curl'
			 'libtre-devel')
provides=("elinks")
conflicts=("elinks")
backup=()
options=()
install=
changelog=
source=("${pkgname}"::'git+http://elinks.cz/elinks.git'
        "msys-platform.patch")
sha256sums=('SKIP'
            '836437f4421efa3931e8401a7e1044836e7fe1e7016870d685e6992c71dc695c')

pkgver() {
	cd ${srcdir}/${pkgname}
	printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${srcdir}/${pkgname}
	patch -p1 -i ${srcdir}/msys-platform.patch
	./autogen.sh
}

build() {
	cd ${srcdir}/${pkgname}
	./configure --prefix=/usr \
		--build=${CHOST} \
		--with-lzma \
		--with-perl \
		--with-python \
		--without-lua
	make -j1
}

check() {
	cd ${srcdir}/${pkgname}
	make -j1 -k check
}

package() {
	cd ${srcdir}/${pkgname}
	make DESTDIR="$pkgdir/" install
	rm -f $pkgdir/usr/share/locale/locale.alias
}
