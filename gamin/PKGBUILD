# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname="gamin"
pkgver=0.1.10
pkgrel=1
pkgdesc="File and directory monitoring system defined to be a subset of the FAM (File Alteration Monitor)"
arch=('i686' 'x86_64')
url="https://people.gnome.org/~veillard/gamin/"
license=('LGPL')
groups=('development')
makedepends=('gcc' 'gtk-doc' 'glib2-devel' 'pcre-devel' 'python2')
optdepends=('python2: for the python module')
depends=()
options=()
source=("ftp://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2"
        001-no-undefined.patch
        002-fix-deprecated-const.patch
        003-python-link.patch)
sha256sums=('a59948b20ce2f14136c013f155abb8c8d51db2ea167c063ff33461e453fec10a'
            'ab3d63b9ec568fbfe597216135b1e210b92299da0d2556be70fa7c01b3600e06'
            '33af6c9a94b459506fc48892c169d89cb4ad7c68e160f0c4bc74475f19ce5a2f'
            '1f11906d4d4dc5159ca647167899ccfb5efec4648ea77b6e96397142293bbab7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ${srcdir}/001-no-undefined.patch
  patch -p1 -i ${srcdir}/002-fix-deprecated-const.patch
  patch -p1 -i ${srcdir}/003-python-link.patch
  autoreconf -ivf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/gamin \
    --disable-server \
    --with-python=/usr/bin/python2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
