# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_realname='pactoys'
pkgname="${_realname}-git"
pkgver=r1.e58a7ac
pkgrel=1
pkgdesc='A set of pacman packaging utilities'
url='https://github.com/renatosilva/pactoys'
groups=('base' 'base-devel')
license=(BSD)
arch=(i686 x86_64)

provides=(${_realname})
conflicts=(${_realname} repman-git updpkgver-git)
replaces=(repman-git updpkgver-git)
depends=(pacman pkgfile wget)
makedepends=(gcc git)
install="${_realname}.install"
source=('git+https://github.com/renatosilva/pactoys')
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_realname}"
    printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
    cd "${srcdir}/${_realname}/repman/native"
    make
}

package() {
    cd "${srcdir}/${_realname}"
    mkdir -p "${pkgdir}/var/cache/pacboy"

    install -Dm755 makepatch/makepatch.sh          "${pkgdir}/usr/bin/makepatch"
    install -Dm755 pacboy/pacboy.sh                "${pkgdir}/usr/bin/pacboy"
    install -Dm755 repman/native/repman.exe        "${pkgdir}/usr/bin/repman.exe"
    install -Dm755 saneman/saneman.sh              "${pkgdir}/usr/bin/saneman"
    install -Dm755 updpkgver/updpkgver.sh          "${pkgdir}/usr/bin/updpkgver"

    install -Dm644 pacboy/pacboy.completion        "${pkgdir}/usr/share/bash-completion/completions/pacboy"
    install -Dm644 LICENSE                         "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -Dm644 repman/native/inih/LICENSE.txt  "${pkgdir}/usr/share/licenses/${_realname}/inih/LICENSE"
}
