# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=crt
_mingw_suff=mingw-w64-cross
pkgname="${_mingw_suff}-${_realname}"
_ver_base=4.0.0
pkgver=4.0.0.6390
pkgrel=1
pkgdesc='MinGW-w64 CRT for cross-compiler'
arch=('i686' 'x86_64')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("${_mingw_suff}-headers")
makedepends=("subversion" "${_mingw_suff}-gcc" "${_mingw_suff}-binutils")
optdepends=()
replaces=()
backup=()
options=('!strip' 'staticlibs' '!emptydirs' '!buildflags')
source=("${_realname}"::"svn+svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-crt")
md5sums=(SKIP)

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
  cd "$srcdir/${_realname}"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
}

build() {
	for _target in ${_targets}; do
    msg "Building ${_target} CRT"
	case "$_target" in
		i686*)
			_crt_configure_args="--disable-lib64 --enable-lib32"
		;;
		x86_64*)
			_crt_configure_args="--disable-lib32 --enable-lib64"
		;;
	esac
    mkdir -p ${srcdir}/crt-${_target} && cd ${srcdir}/crt-${_target}
    ${srcdir}/${_realname}/configure \
		--build=${CHOST} \
		--prefix=/opt/${_target} \
		--host=${_target} \
		--enable-wildcard \
        ${_crt_configure_args}
    make
	done
}

package() {
	for _target in ${_targets}; do
    	msg "Installing ${_target} crt"
    	cd ${srcdir}/crt-${_target}
    	make DESTDIR=${pkgdir} install
	done
}

