# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('libmetalink' 'libmetalink-devel')
pkgver=0.1.2
pkgrel=1
pkgdesc="Metalink XML parser library."
groups=('libraries')
arch=('i686' 'x86_64')
license=('MIT')
url="https://launchpad.net/libmetalink"
depends=('msys2-runtime' 'libexpat' 'sh')
makedepends=('libexpat-devel' 'pkg-config')
source=(${url}/trunk/packagingfix/+download/${pkgname}-${pkgver}.tar.bz2
        libmetalink-0.1.2-msys2.patch)
sha1sums=('fcc8c7960758c040b8b5f225efeb3f22bff14e40'
          'dd7f17a4cefb161c41388b87eda247f04c750296')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"

  patch -p1 -i $srcdir/libmetalink-0.1.2-msys2.patch
  autoreconf -fi
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  ./configure --build=${CHOST} --prefix=/usr
  
  make
  make DESTDIR=${srcdir}/dest install
}

package_libmetalink() {
  groups=('libraries')
  
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_libmetalink-devel() {
  pkgdesc="libmetalink headers and libraries"
  groups=('development')
  options=('!staticlibs')
  depends=("libmetalink=${pkgver}")
 
  mkdir -p ${pkgdir}/usr
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
