# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Jürgen Pfeifer <juergen@familiepfeifer.de>

pkgname=lftp
pkgver=4.5.0
pkgrel=1
pkgdesc="Sophisticated command line based FTP client"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gcc-libs' 'libreadline' 'openssh' 'expat' 'openssl')
makedepends=('gcc' 'libreadline-devel' 'openssl-devel')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc}
        lftp-4.4.15-msysize.patch
        lftp-4.4.15-no-undefined.patch)
md5sums=('cddbbf3f958047f7ce5a2e4386b36fc8'
         'SKIP'
         '9b34d1d402d804cf3660d8a9fd0c00a4'
         '70767b69404fdd0b8d6c7f185f3b5b3b')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lftp-4.4.15-msysize.patch
  patch -p1 -i ${srcdir}/lftp-4.4.15-no-undefined.patch
  
  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-gnutls \
    --with-openssl \
    --without-included-regex \
    --disable-rpath \
    --disable-static
  make
}

package() {

  cd ${srcdir}/build-${CARCH}
  make DESTDIR=${pkgdir} install
  mv ${pkgdir}/etc ${pkgdir}/usr/
  #rm -rf ${pkgdir}/usr/lib
}
