# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Jürgen Pfeifer <juergen@familiepfeifer.de>

pkgname=lftp
pkgver=4.4.15
pkgrel=1
pkgdesc="Sophisticated command line based FTP client"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gcc-libs' 'libreadline' 'openssh' 'expat' 'openssl')
builddepends=('gcc' 'libreadline-devel' 'libopenssl-devel')
optdepends=('perl: needed for convert-netscape-cookies and verify-file')
url="http://lftp.yar.ru/"
backup=('etc/lftp.conf')
source=(http://lftp.yar.ru/ftp/${pkgname}-${pkgver}.tar.xz{,.asc}
		lftp-4.4.15-msysize.patch)
md5sums=('f19cc0cea1d427aa2a952b27661b91e5'
         'SKIP'
         '9b34d1d402d804cf3660d8a9fd0c00a4')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/lftp-4.4.15-msysize.patch
  
  autoreconf -fi
}

build() {
  mkdir -p ${srcdir}/build-${CARCH} && cd ${srcdir}/build-${CARCH}
  ../${pkgname}-${pkgver}/configure \
    --build=${CHOST} \
    --prefix=/usr \
    --without-gnutls \
    --with-openssl \
    --without-included-regex \
    --disable-rpath \
    --disable-static
  make
}

package() {

  cd ${srcdir}/build-${CARCH}
  make DESTDIR=${pkgdir} install
  mv ${pkgdir}/etc ${pkgdir}/usr/
  #rm -rf ${pkgdir}/usr/lib
}
