# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ack
pkgname=perl-${_realname}
pkgver=2.10
pkgrel=1
pkgdesc="A Perl-based grep replacement, aimed at programmers with large trees of heterogeneous source code"
arch=('any')
url="http://betterthangrep.com/"
license=('GPL' 'PerlArtistic')
depends=('perl-File-Next')
groups=('perl-modules')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/P/PE/PETDANCE/${_realname}-${pkgver}.tar.gz"
        'ack-2.10-msys2.patch')
md5sums=('4581d79ecb12b544f363f0f162aaaeb5'
         'd90248c7cebca315007c886dc44b2379')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i ${srcdir}/ack-2.10-msys2.patch
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # some standalone tests are failing
  make test || true
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
