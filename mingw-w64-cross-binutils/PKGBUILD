# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Martell Malone <martellmalone@gmail.com>

_realname=binutils
_mingw_suff=mingw-w64-cross
pkgname=("${_mingw_suff}-${_realname}")
#_basever=2.24.51
#pkgver=${_basever}.20131119
pkgver=2.26
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${_mingw_suff}-toolchain" "${_mingw_suff}")
depends=("libiconv" "zlib")
checkdepends=('dejagnu' 'bc')
makedepends=("libiconv" "zlib")
options=('staticlibs' '!distcc' '!ccache' '!buildflags')
#install=binutils.install
source=(#"https://mirrors.kernel.org/sources.redhat.com/binutils/snapshots/binutils-${_basever}.tar.bz2"
        https://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0007-armv7-w64-mingw32.patch
        0008-fix-libiberty-makefile.mingw.patch
        0009-fix-libiberty-configure.mingw.patch
        0110-binutils-mingw-gnu-print.patch
        binutils-2.26-msys2.patch
        27aaeda.diff
        coff-linker-weak-nt-externals.patch
        gas-aux-nt-weak-externals.patch
        objcopy-weak-nt-externals2local.patch)
sha256sums=('c2ace41809542f5237afc7e3b8f32bb92bc7bc53c6232a84463c423b0714ecd9'
            'SKIP'
            '93296b909e1a4f9d8a4bbe2437aafa17ca565ef6642a9812b0360c05be228c9d'
            '2a6bfc6dbb1a951adf051ebe3e2b4e2872dfed0c177af386ac21ce8dfd289683'
            '8f15a795b013abfcf56f76e295cfb0e0392576896e28575d7f917012c469fcd9'
            'b79c51f566196a41ad05dacf7fd4b9ac15c0fcaab1fcf8381aeb71b5cf2a691e'
            '4849fe969fc7abb6b6dfcef4d4c6506f23831d86ba22c6dbf8d3039212654837'
            'e33541d4fc7bbf96e41ad2636f57f521f73d364ef72968a3ea2fa6c8596c9ebb'
            '4a54aa4a32aaa9ca79219cec0f86ef1481ed96a81312959a30bfaddf79436b7f'
            'ec32f5a63fe00315586d1502bd91fc0c700ce63174fc5eade3ae96420140b134'
            '76663a2493f81e2306140259a2e4306b7cc4592fc4381951df87b5aa52c0fb62'
            'a5764911df3bde0e51fe2c244cac38a2f4169081017796b8cc9f214d34c17930'
            '05933fb77e5577257fe1d7a6ff2e701e93fd43ce417a29165bc6a71da5326459'
            )

_targets="i686-w64-mingw32 x86_64-w64-mingw32 armv7-w64-mingw32"

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i "${srcdir}"/0001-enable-gold-on.mingw32.patch
  patch -p1 -i "${srcdir}"/0002-check-for-unusual-file-harder.patch
  patch -p1 -i "${srcdir}"/0007-armv7-w64-mingw32.patch
  patch -p1 -i "${srcdir}"/0008-fix-libiberty-makefile.mingw.patch
  patch -p1 -i "${srcdir}"/0009-fix-libiberty-configure.mingw.patch
  patch -p1 -i "${srcdir}"/0110-binutils-mingw-gnu-print.patch
  patch -p1 -i "${srcdir}"/coff-linker-weak-nt-externals.patch
  patch -p1 -i "${srcdir}"/gas-aux-nt-weak-externals.patch
  patch -p1 -i "${srcdir}"/objcopy-weak-nt-externals2local.patch
  patch -p1 -i "${srcdir}"/binutils-2.26-msys2.patch

  #do not install libiberty
  #sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # Upstream patches
  patch -p1 -i "${srcdir}"/27aaeda.diff

}

build() {
  for _target in ${_targets}; do
    mkdir -p ${srcdir}/binutils-build-${_target} && cd ${srcdir}/binutils-build-${_target}

    if [ "${_target}" != "armv7-w64-mingw32" ]; then
      local _conf='--enable-lto'
    else
      local _conf=''
    fi

    if [ "${_target}" = "x86_64-w64-mingw32" ]; then
     _conf+=' --enable-64-bit-bfd'
    fi

    ${srcdir}/binutils-${pkgver}/configure \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${_target} \
      --prefix=/opt \
      --disable-werror \
      --with-libiconv-prefix=/usr \
      $_conf

    make
  done
}

check() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}

    # unset LDFLAGS as testsuite makes assumptions about which ones are active
    # do not abort on errors - manually check log files
    make LDFLAGS="" -k check || true
  done
}

package() {
  for _target in ${_targets}; do
    cd ${srcdir}/binutils-build-${_target}
    make DESTDIR=${pkgdir} install

    # Remove unwanted files
    #rm ${destdir}/usr/share/info/{configure,standards}.info

    # Add some useful headers
    #install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/opt/include
    #install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/opt/include
  done
}
