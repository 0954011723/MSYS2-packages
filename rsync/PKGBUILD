# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=rsync
pkgver=3.1.0
pkgrel=2
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
groups=('net-utils')
license=('GPL3')
depends=('perl')
source=("http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
        "http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz.asc"
        rsync-3.1.0-msys2.patch
        CVE-2014-2855.patch)
md5sums=('3be148772a33224771a8d4d2a028b132'
         'SKIP'
         '6964f315300b3cfe8f663d1386628368'
         'dacfe77bd72fbf6b6ba65c741c57f74c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -i ${srcdir}/rsync-3.1.0-msys2.patch
  patch -p1 -i ${srcdir}/CVE-2014-2855.patch
  
  autoreconf -fi
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --build=${CHOST} \
        --prefix=/usr \
        --without-included-zlib
    make man all
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make test
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}
