# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=binutils
#_basever=2.24.51
#pkgver=${_basever}.20131119
pkgver=2.24
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('msys2-devel')
depends=('libiconv' 'libintl' 'zlib')
checkdepends=('dejagnu' 'bc')
makedepends=('libiconv-devel' 'gettext-devel' 'zlib-devel')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(#http://sourceware.mirrors.tds.net/pub/sourceware.org/binutils/snapshots/binutils-${_basever}.tar.bz2
        ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        #binutils-2.23.2-texinfo-5.0.patch
        cofflink.patch
		2.23.52-install-libiberty.patch
        binutils-trunk-msys2.patch)
md5sums=('e0f71a7b2ddab0f8612336ac81d9636b'
         'SKIP'
         '65b166affc5cc9a422a9da90dd95bb9f'
         'bbedd06f1acf9cab0a8dda20380cf26b'
         '6f7175b7ed29bc597f0d300500e2b651')

prepare() {
  cd ${srcdir}/binutils-$pkgver
  if [ "${CARCH}" = "x86_64" ]; then
	msg "Applying reloc patch"
	patch -p0 -i $srcdir/cofflink.patch
  fi
  
  patch -p2 -i $srcdir/2.23.52-install-libiberty.patch
  patch -p1 -i $srcdir/binutils-trunk-msys2.patch
  
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
  fi
  ${srcdir}/binutils-${pkgver}/configure --build=${CHOST} \
    --prefix=/usr \
    --disable-werror \
	$_conf \
    --without-libiconv-prefix \
    --without-libintl-prefix
#	--enable-install-libiberty 
  make
}

check() {
  cd ${srcdir}/binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make DESTDIR=${pkgdir} install
  cp libiberty/libiberty.a ${pkgdir}/usr/lib
  
  # Remove unwanted files
  rm ${pkgdir}/usr/share/info/{configure,standards}.info
  
  # Add some useful headers
  #install -m644 ${srcdir}/binutils-${_basever}/include/libiberty.h ${pkgdir}/usr/include
  #install -m644 ${srcdir}/binutils-${_basever}/include/demangle.h ${pkgdir}/usr/include
}
