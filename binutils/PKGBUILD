# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=binutils
_basever=2.24.51
pkgver=${_basever}.20140408
#pkgver=2.24.51
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('msys2-devel')
depends=('libiconv' 'libintl' 'zlib')
checkdepends=('dejagnu' 'bc')
makedepends=('libiconv-devel' 'gettext-devel' 'zlib-devel')
options=('staticlibs' '!distcc' '!ccache')
install=binutils.install
source=(#http://sourceware.mirrors.tds.net/pub/sourceware.org/binutils/snapshots/binutils-${_basever}.tar.bz2
    #ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
    http://cygwin.parentingamerica.com/x86_64/release/binutils/binutils-2.24.51-2-src.tar.xz
    02-x86-64-pc32-relocs.patch
    2.23.52-install-libiberty.patch
    binutils-trunk-msys2.patch)
md5sums=('7f198636895e8e17489fc7d86342f148'
         'e7e91814b32f4c4c63d4c777f5eabf7f'
         'bbedd06f1acf9cab0a8dda20380cf26b'
         'ffafa01f790d8b338951382a7eee2759')

prepare() {
  cd ${srcdir}/binutils-$_basever-2
  #if [ "${CARCH}" = "x86_64" ]; then
  #  msg "Applying reloc patch"
  #  patch -p0 -i $srcdir/02-x86-64-pc32-relocs.patch
  #fi
  
  #patch -p2 -i $srcdir/2.23.52-install-libiberty.patch
  patch -p1 -i $srcdir/binutils-trunk-msys2.patch
  
  rm -rf gdb
  
  #autoreconf -fi
  
  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
  fi
  ${srcdir}/binutils-${_basever}-2/configure \
    --prefix=/usr \
    --build=${CHOST} \
    --host=${CHOST} \
    --target=${CHOST} \
    --disable-werror \
    $_conf \
    --without-libiconv-prefix \
    --without-libintl-prefix
    #--enable-install-libiberty 
  make -j1
}

check() {
  cd ${srcdir}/binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make DESTDIR=${pkgdir} install
  cp libiberty/libiberty.a ${pkgdir}/usr/lib
  
  # Remove unwanted files
  rm ${pkgdir}/usr/share/info/{configure,standards}.info
  
  # Add some useful headers
  #install -m644 ${srcdir}/binutils-${_basever}/include/libiberty.h ${pkgdir}/usr/include
  #install -m644 ${srcdir}/binutils-${_basever}/include/demangle.h ${pkgdir}/usr/include
}
