# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('liblzo2' 'liblzo2-devel')
pkgver=2.07
pkgrel=1
pkgdesc="Portable lossless data compression library"
arch=('i686' 'x86_64')
url="http://www.oberhumer.com/opensource/lzo"
license=('GPL')
depends=()
groups=('libraries' 'compression')
source=(http://www.oberhumer.com/opensource/lzo/download/lzo-${pkgver}.tar.gz
        lzo-2.07-msys2.patch
        configure-2.07.patch)
options=(!libtool)
md5sums=('4011935e95171e78ad4894f7335c982a'
         'ae2b844f8a17d320aaa3511e8d355562'
         'db3ead3fb5590275838d3080ea7342d1')

prepare() {
  cd "${srcdir}/lzo-${pkgver}"
  patch -p1 -i ${srcdir}/lzo-2.07-msys2.patch
  patch -p0 -i ${srcdir}/configure-2.07.patch
  
  autoreconf -fi
}

build() {
  cd "${srcdir}/lzo-${pkgver}"
  
  ./configure --prefix=/usr --enable-shared
  
  make
  make DESTDIR=${srcdir}/dest install
}

check() {
  cd "${srcdir}/lzo-${pkgver}"
  make test # Larger test
  make check
}

package_liblzo2() {
  mkdir -p ${pkgdir}/usr
  
  cp -rf ${srcdir}/dest/usr/bin ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
}

package_liblzo2-devel() {
  pkgdesc="Liblzo2 headers and libraries"
  options=('staticlibs')
  groups=('development')
  depends=("liblzo2=${pkgver}")
  
  mkdir -p ${pkgdir}/usr
  
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib ${pkgdir}/usr/
}
