# Maintainer: Renato Silva <br.renatosilva@gmail.com>

_name=repman
pkgname=${_name}-git
pkgdesc="Pacman repository manager"
url="https://github.com/renatosilva/repman"
license=(GPL)

arch=(i686 x86_64)
pkgver=r18.69da11c
pkgrel=2

groups=(base)
depends=(pacman)
makedepends=(git)
provides=(${_name})
conflicts=(${_name})
source=("git+$url")
md5sums=(SKIP)

pkgver() {
    cd "$srcdir/${_name}"
    printf "r%s.%s" $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

build() {
    cd "$srcdir/${_name}/native"
    make
}

package() {
    cd "$srcdir/${_name}/native"
    mkdir -p "$pkgdir/usr/bin"
    mkdir -p "${pkgdir}/usr/share/licenses/repman/inih"
    cp -v repman.exe "$pkgdir/usr/bin"
    cp -v inih/LICENSE.txt "${pkgdir}/usr/share/licenses/repman/inih/LICENSE"
}
