# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=groff
pkgver=1.22.2
pkgrel=1
pkgdesc="GNU troff text-formatting system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/groff/groff.html"
license=('GPL')
groups=('base-devel')
depends=('perl' 'libgcc' 'libstdcpp')
makedepends=()
optdepends=()
source=(ftp://ftp.gnu.org/gnu/groff/groff-$pkgver.tar.gz{,.sig}
        site.tmac
        groff-1.22.2-msys2.patch
        0001-fix-mom-pdf-symlink.patch)
options=('!docs' '!emptydirs')
install=groff.install
md5sums=('9f4cd592a5efc7e36481d8d8d8af6d16'
         'SKIP'
         'a1fedafd7863b37968d32ad9ae1d8c13'
         'cd30f724b1f2e7af13abd201b14568d0'
         'a611a242c4e72ff789a61c5a10962cb2')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  
  patch -p2 -i $srcdir/0001-fix-mom-pdf-symlink.patch
  patch -p1 -i $srcdir/groff-1.22.2-msys2.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-x
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  #rm -f $pkgdir/usr/lib/charset.alias

  # FS33760 - TERMCAP variables not followed
  # TODO: everyone is doing this - find out why upstream does not...
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/man.local
  cat $srcdir/site.tmac >> \
    $pkgdir/usr/share/groff/site-tmac/mdoc.local
}
