# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('msys2-w32api-headers')
_ver_base=4.0.0
pkgver=4.0.0.4318.5de42ab
pkgrel=1
pkgdesc="Win32 API headers for MSYS2 32bit toolchain"
arch=('i686' 'x86_64')
url="http://mingw-w64.sourceforge.net/"
license=('custom')
groups=('msys2-devel')
depends=()
makedepends=('git' 'mingw-w64-cross-gcc')
#provides=($pkgname)
#conflicts=($pkgname)
options=('staticlibs' '!buildflags')
source=('msys2-w32api-headers'::'git://git.code.sf.net/p/mingw-w64/mingw-w64')
md5sums=('SKIP')

MINGW_CHOST=${CARCH}-w64-mingw32

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s.%s" "$_ver_base" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
build() {
  cd $srcdir/$pkgname/mingw-w64-headers
  
  ./configure \
     --build=${CHOST} \
     --host=${MINGW_CHOST} \
     --prefix=/usr \
     --enable-sdk=all \
     --enable-w32api

  make
}

package() {
  cd $srcdir/$pkgname/mingw-w64-headers
  
  make DESTDIR=${pkgdir} install
}
