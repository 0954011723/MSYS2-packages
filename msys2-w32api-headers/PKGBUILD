# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('msys2-w32api-headers')
_ver_base=4.0.0
pkgver=4.0.0.6365
pkgrel=1
pkgdesc="Win32 API headers for MSYS2 32bit toolchain"
arch=('i686' 'x86_64')
url="http://mingw-w64.sourceforge.net/"
license=('custom')
groups=('msys2-devel')
depends=()
makedepends=('subversion')
provides=($pkgname)
conflicts=($pkgname)
options=('staticlibs')
source=('msys2-w32api-headers'::'svn://svn.code.sf.net/p/mingw-w64/code/trunk/mingw-w64-headers')
md5sums=(SKIP)

MINGW_CHOST=${CARCH}-w64-mingw32

change_path() {
  ORIG_PATH=$PATH
 
  case ${CARCH} in
    i686)
      export PATH=/mingw32/bin:$PATH
    ;;
    x86_64)
      export PATH=/mingw64/bin:$PATH
    ;;
  esac
}

restore_path() {
  export PATH=$ORIG_PATH
  unset ORIG_PATH
}

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "%s.%s" "${_ver_base}" "${ver//[[:alpha:]]}"
  
}
 
build() {
  cd $srcdir/$pkgname
  
  change_path
  ./configure --build=${MINGW_CHOST} \
     --target=${MINGW_CHOST} \
     --host=${MINGW_CHOST} \
     --prefix=${srcdir}/dest \
     --enable-sdk=all \
     --enable-w32api

  make
  restore_path
}

package() {
  cd $srcdir/$pkgname
  
  change_path
  make install
  restore_path
  
  mkdir -p $pkgdir/usr
  cp -rf ${srcdir}/dest/* $pkgdir/usr/
}
